[aliases]
n0=; Milionerowy QuizÂ® by KrzS&Dizzy
n1=; Radze tu nic nie zmieniac
n2=; mod by wilk (odpowiadanie na # i/lub priv, rozbudowany /mtryb, tryb 1234 lub dowolnie wybrany, alias /mdelay, wylaczenie mnoznika/trimba/bonusow, zapisywanie ustawien w pliku)
n3=
n4=//mon {
n5=  if ($1 == $null) {
n6=    echo -a 9,1 MILIONEROWY 8QUIZ Podaj nazwe kanalu na ktorym chcesz grac, np:  "/mon #kanal"
n7=    halt
n8=  }
n9=  unset %ml_*
n10=
n11=  set %ml_version 3.2
n12=  set %ml_answer_time 20
n13=  set %ml_question_delay 20
n14=  set %ml_question_number 0
n15=  set %ml_points_for_answers 1
n16=  set %ml_public 0
n17=  set %ml_a A
n18=  set %ml_b B
n19=  set %ml_c C
n20=  set %ml_d D
n21=  set %ml_no_multiplier 0
n22=  set %ml_no_trimbo 0
n23=  set %ml_points_first 3
n24=  set %ml_points_second 2
n25=  set %ml_points_rest 1
n26=  set %ml_points_bonus 1
n27=
n28=  load -rs milioner2.ini
n29=  set %ml_quiz_channel $$1
n30=  if ($hget(total_points)) {
n31=    hfree total_points
n32=  }
n33=  if ($hget(multipliers)) {
n34=    hfree multipliers
n35=  }
n36=  if ($hget(correct_answers)) {
n37=    hfree correct_answers
n38=  }
n39=  if ($hget(incorrect_answers)) {
n40=    hfree incorrect_answers
n41=  }
n42=  if ($hget(locked_players)) {
n43=    hfree locked_players
n44=  }
n45=
n46=  wczytajconf
n47=
n48=  echo 4Milionerowy Quiz (wersja %ml_version $+ ) wystartowal ! 
n49=  echo 4Spis komend /mkom
n50=  msg %ml_quiz_channel 1,1 ............9MILIONEROWY8 Quiz 8,1(wersja %ml_version $+ ) wystartowal na 0 $+ %ml_quiz_channel 1............ $adjust_length(11, %ml_quiz_channel)
n51=  msg %ml_quiz_channel 2,2 ....8,2 !zasady - zasady,2,2..8,2!ile - miejsce,2,2..8,2punktacja automatycznie co 5 pytan  2......
n52=  if (%ml_public == 0) {
n53=    msg %ml_quiz_channel 2,2 ...........15,2 >>> odpowiedzi udzielamy PRYWATNIE puszczajacemu quiz <<< 2............
n54=  }
n55=  else {
n56=    msg %ml_quiz_channel 2,2 .....15,2 >>> odpowiedzi mozna udzielac takze prywatnie puszczajacemu quiz <<< 2.......
n57=  }
n58=  if ((%ml_a != A) || (%ml_b != B) || (%ml_c != C) || (%ml_d != D)) {
n59=    msg %ml_quiz_channel 2,2 .....................15,2 >>> symbole odpowiedzi to: %ml_a $+ / $+ %ml_b $+ / $+ %ml_c $+ / $+ %ml_d <<< 2.....................
n60=  }
n61=  if (%ml_points_for_answers == 1) {
n62=    msg %ml_quiz_channel 2,2 .................15,2 >>> tryb "punkt za odpowiedz" jest aktywny <<< 2.................
n63=  }
n64=
n65=  set %ml_start_time $ctime
n66=}
n67=
n68=//mtryb {
n69=  if ($1 == punkt) {
n70=    set %ml_points_for_answers 1
n71=    msg %ml_quiz_channel 2,2 ............................11,2 Tryb: punkt za odpowiedz 2............................
n72=  }
n73=  elseif ($1 == normal) {
n74=    set %ml_points_for_answers 0
n75=    msg %ml_quiz_channel 2,2 ............................11,2 Tryb: punktowanie zwykle 2............................
n76=  }
n77=  elseif ($1 == pub) {
n78=    set %ml_public 1
n79=    msg %ml_quiz_channel 2,2 .................11,2 Tryb: mozna odpowiadac na kanale lub prywatnie 2.................
n80=  }
n81=  elseif ($1 == priv) {
n82=    set %ml_public 0
n83=    msg %ml_quiz_channel 2,2 ........................11,2 Tryb: odpowiedzi TYLKO prywatnie 2........................
n84=  }
n85=  elseif ($1 == mnoznik) {
n86=    if (%ml_no_multiplier == 0) {
n87=      set %ml_no_multiplier 1
n88=      msg %ml_quiz_channel 2,2 ..................11,2 Tryb: bonusy za serie odpowiedzi - wylaczone 2..................
n89=    }
n90=    else {
n91=      set %ml_no_multiplier 0
n92=      msg %ml_quiz_channel 2,2 ...................11,2 Tryb: bonusy za serie odpowiedzi - aktywne 2...................
n93=    }
n94=  }
n95=  elseif ($1 == trimbo) {
n96=    if (%ml_no_trimbo == 0) {
n97=      set %ml_no_trimbo 1
n98=      msg %ml_quiz_channel 2,2 .....................11,2 Tryb: mnoznik x3 (TRIMBO) - wylaczony 2......................
n99=    }
n100=    else {
n101=      set %ml_no_trimbo 0
n102=      msg %ml_quiz_channel 2,2 ......................11,2 Tryb: mnoznik x3 (TRIMBO) - dostepny 2......................
n103=    }
n104=  }
n105=  elseif ($len($1) == 4) {
n106=    var %ml_tmp_1 = $upper($left($1, 1))
n107=    var %ml_tmp_2 = $upper($mid($1, 2, 1))
n108=    var %ml_tmp_3 = $upper($mid($1, 3, 1))
n109=    var %ml_tmp_4 = $upper($right($1, 1))
n110=    if ((%ml_tmp_1 != %ml_tmp_2) && (%ml_tmp_1 != %ml_tmp_3) && (%ml_tmp_1 != %ml_tmp_4) && (%ml_tmp_2 != %ml_tmp_3) && (%ml_tmp_2 != %ml_tmp_4) && (%ml_tmp_3 != %ml_tmp_4)) {
n111=      set %ml_a %ml_tmp_1
n112=      set %ml_b %ml_tmp_2
n113=      set %ml_c %ml_tmp_3
n114=      set %ml_d %ml_tmp_4
n115=      msg %ml_quiz_channel 2,2 ............................11,2 Tryb: odpowiedzi %ml_a $+ / $+ %ml_b $+ / $+ %ml_c $+ / $+ %ml_d 2............................
n116=    }
n117=  }
n118=  zapiszconf
n119=}
n120=
n121=//moff {
n122=  .timerAnswers off
n123=  .timerWarning off
n124=  .timerResult off
n125=  .timerStats off
n126=  .timerNext off
n127=
n128=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n129=  msg %ml_quiz_channel 1,1 ...........9MILIONEROWY8 Quiz 8,1(wersja %ml_version by KrzS&Dizzy&wilk) zakonczony 1...........
n130=  if (%ml_question_number == 1) {
n131=    msg %ml_quiz_channel 2,2 ......................8,2 Czas gry: 0,1 %ml_question_number 8,2 runda w 0,1 %quiz_duration 2,2 $& 
n132=      $adjust_length(3, %ml_question_number) $+ $adjust_length(32, %quiz_duration)
n133=  }
n134=  else {
n135=    msg %ml_quiz_channel 2,2 ......................8,2 Czas gry: 0,1 %ml_question_number 8,2 rund w 0,1 %quiz_duration 2,2 $&
n136=      $adjust_length(3, %ml_question_number) $+ $adjust_length(33, %quiz_duration)
n137=  }
n138=  msg %ml_quiz_channel 2,2 .......................8,2 Sciagaj z: 0,2http://www.quizpl.net 2,2.........................
n139=  .unload -rs milioner2.ini
n140=  unset %ml_*
n141=  .rlevel =2
n142=}
n143=
n144=/mplik {
n145=  set %ml_file $1-
n146=  if ($exists(%ml_file) == $false) {
n147=    echo 9,1 MILIONEROWY 08Quiz:8,2 Plik 0,1 %ml_file 8,2 nie istnieje 2,2 $adjust_length(42, %ml_file)
n148=    halt
n149=  }
n150=
n151=  set %ml_number_of_questions $lines(%ml_file) / 5
n152=  set %ml_line_number 0 
n153=  echo 9,1 MILIONEROWY 08Quiz:8,2 Plik z pytaniami: 0,1 $nopath(%ml_file) 08,02, liczba pytan: 0,1 %ml_number_of_questions 2,2 $&
n154=    $adjust_length(22, $nopath(%ml_file)) $+ $adjust_length(3, %ml_number_of_questions)
n155=
n156=  if (%ml_question_delay == $null) {
n157=    set %ml_question_delay 10
n158=  }
n159=
n160=  set %ml_running 1
n161=  .timer 1 3 next_question
n162=}
n163=
n164=get_number_of_dots {
n165=  var %dot_length $len($2-)
n166=  dec %dot_length 7
n167=  %dot_length = $1 - %dot_length
n168=  return %dot_length 
n169=}
n170=
n171=add_dots {
n172=  var %dots = .
n173=  while ($len(%dots) < $get_number_of_dots($1,$2-)) {
n174=    %dots = %dots $+ . 
n175=  }
n176=  return %dots
n177=}
n178=
n179=; ile znakow, tekst
n180=adjust_length {
n181=  if ($len($2-) >= $1) {
n182=    return
n183=  }
n184=
n185=  var %dots = .
n186=  var %total_length = $len($2-) + $len(%dots)
n187=  while (%total_length < $1) {
n188=    %dots = %dots $+ .
n189=    inc %total_length
n190=  }
n191=
n192=  return %dots
n193=}
n194=
n195=antigoogle {
n196=  var %replacement =  $+ $2 $+ a08
n197=  return $replace($1,$chr(32),%replacement)
n198=}
n199=
n200=get_point_word {
n201=  if ($1 == $null) {
n202=    return
n203=  }
n204=  var %number = $abs($1)
n205=
n206=  if (%number == 1) {
n207=    return punkt
n208=  }
n209=  elseif ((%number > 10) && (%number < 20)) {
n210=    return punktow
n211=  }
n212=  elseif (($right(%number,1) > 1) && ($right(%number,1) < 5)) {
n213=    return punkty
n214=  }
n215=  else {
n216=    return punktow
n217=  }
n218=}
n219=
n220=read_line {
n221=  var %line $1
n222=  var %file_name $2-
n223=  var %command -l $+ %line 
n224=  return $read %command %file_name
n225=}
n226=
n227=next_question {
n228=  if (%ml_running == 0) {
n229=    return
n230=  }
n231=
n232=  inc %ml_line_number
n233=  var %current_question = $read_line(%ml_line_number, %ml_file)
n234=  if (%ml_line_number < $lines(%ml_file)) {
n235=
n236=    hmake answers
n237=    hmake random_answers
n238=    var %i
n239=
n240=    ; reading answers
n241=    %i = 1
n242=    while (%i <= 4) {
n243=      inc %ml_line_number
n244=      hadd answers $chr($calc(64 + %i)) $read_line(%ml_line_number, %ml_file)
n245=      inc %i
n246=    }
n247=
n248=    ; randomizing answers
n249=    %i = 1
n250=    while (%i <= 4) {
n251=      var %answer_number = $rand(1, $calc(5 - %i))  
n252=      var %answer_key = $hget(answers, %answer_number).item
n253=      var %answer_description = $+($chr(3), 08, %ml_ [ $+ [ $chr($calc(96 + %i)) ] ], :, $chr(32), $chr(3), 08, $hget(answers, %answer_number).data)
n254=
n255=      hadd random_answers $chr($calc(64 + %i)) %answer_description      
n256=      if (%answer_number == 1 && %ml_round_correct_answer == $null) {
n257=        set %ml_round_correct_answer %ml_ [ $+ [ $chr($calc(96 + %i)) ] ]
n258=        set %ml_round_correct_answer_description $hget(answers, %answer_number).data
n259=      }
n260=      hdel answers %answer_key
n261=
n262=      inc %i
n263=    }
n264=
n265=    inc %ml_question_number
n266=    set %ml_round_question %current_question
n267=    set %ml_round_answer_a $hget(random_answers, A)
n268=    set %ml_round_answer_b $hget(random_answers, B)
n269=    set %ml_round_answer_c $hget(random_answers, C)
n270=    set %ml_round_answer_d $hget(random_answers, D)
n271=    $display_question
n272=
n273=    hfree random_answers
n274=    hfree answers
n275=  }
n276=  else {
n277=    end_of_questions
n278=  }
n279=}
n280=
n281=display_question {
n282=  msg %ml_quiz_channel 8,1Pytanie $+ $chr(32) $+ %ml_question_number $+ / $+ %ml_number_of_questions $+ :8,1 $antigoogle(%ml_round_question, 01) 1,1 $add_dots(75, %ml_round_question)
n283=  .timerAnswers 1 3 display_answers
n284=}
n285=
n286=display_answers {
n287=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_a, 02) $+ 2,2 $add_dots(45, %ml_round_answer_a) 1,1.08,02 $antigoogle(%ml_round_answer_b, 02) $+ 2,2 $add_dots(45, %ml_round_answer_b)
n288=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_c, 02) $+ 2,2 $add_dots(45, %ml_round_answer_c) 1,1.08,02 $antigoogle(%ml_round_answer_d, 02) $+ 2,2 $add_dots(45, %ml_round_answer_d)
n289=  echo 4 Prawidlowa odpowiedz to: %ml_round_correct_answer - %ml_round_correct_answer_description
n290=  if (%ml_answer_time > 10) {
n291=    .timerWarning 1 $calc(%ml_answer_time - 10) display_warning
n292=  }
n293=  .timerResult 1 %ml_answer_time display_result
n294=  set %ml_round_running 1
n295=}
n296=
n297=display_warning {
n298=  msg %ml_quiz_channel 8,1Uwaga:8,2 zostalo 10 sekund
n299=}
n300=
n301=; nick, 1 poprawna
n302=//register_answer {
n303=  var %points
n304=  if (!%ml_round_answered_first) {
n305=    %points = %ml_points_first
n306=    set %ml_round_answered_first 1
n307=  }
n308=  elseif (!%ml_round_answered_second) {
n309=    %points = %ml_points_second
n310=    set %ml_round_answered_second 1
n311=  }
n312=  else {
n313=    %points = %ml_points_rest
n314=  }
n315=  if ($2 != 1) {
n316=    %points = -1 * %points
n317=    if (!$hget(incorrect_answers, $1)) {
n318=      hadd -m incorrect_answers $1 1
n319=    }
n320=    else {
n321=      var %ile = $hget(incorrect_answers, $1) + 1
n322=      hadd -m incorrect_answers $1 %ile
n323=    }
n324=  }
n325=  else {
n326=    if (!$hget(correct_answers, $1)) {
n327=      hadd -m correct_answers $1 1
n328=    }
n329=    else {
n330=      var %ile = $hget(correct_answers, $1) + 1
n331=      hadd -m correct_answers $1 %ile
n332=    }
n333=  }
n334=
n335=  hadd -m question_points $1 %points
n336=}
n337=
n338=; nick, answer
n339=//question_answered {
n340=  if (%ml_round_running == 1 && $1 ison %ml_quiz_channel && !$hget(question_points, $1) && !$hget(locked_players, $1)) {
n341=    if (%ml_round_correct_answer == $2) {
n342=      register_answer $nick 1
n343=    }
n344=    else {
n345=      register_answer $nick 0
n346=    }
n347=  }
n348=}
n349=
n350=//display_result {
n351=  set %ml_round_running 0
n352=  msg %ml_quiz_channel 8,1Koniec Czasu:8,2 Prawidlowa odpowiedz to:9,2 %ml_round_correct_answer - %ml_round_correct_answer_description
n353=  var %i
n354=  var %number_of_correct_answers = 0
n355=
n356=  %i = 1
n357=  while ($hget(question_points, %i).item) {
n358=    if ($hget(question_points, %i).data > 0) {
n359=      inc %number_of_correct_answers
n360=    }
n361=    inc %i
n362=  }
n363=
n364=  ; aktualizacja punktow
n365=  hmake point_descriptions
n366=  %i = 1
n367=  while ($hget(question_points, %i).item) {
n368=    var %player = $hget(question_points, %i).item
n369=    var %points = $hget(question_points, %i).data
n370=    var %multiplier 1
n371=    var %multiplier_str
n372=
n373=    if ((%points > 0) && (%number_of_correct_answers == 1) && (%ml_points_bonus > 0)) {
n374=      inc %points %ml_points_bonus
n375=      %multiplier_str = BONUS
n376=    }
n377=    if (($hget(multipliers, %player) > 1) && (%ml_no_multiplier == 0)) {
n378=      %multiplier = $hget(multipliers, %player)
n379=      if (%multiplier_str != $null) {
n380=        %multiplier_str = %multiplier_str $+ , $+ $chr(32)
n381=      }
n382=      if (%multiplier == 2) {
n383=        %multiplier_str = %multiplier_str COMBO
n384=      }
n385=      else {
n386=        if (%ml_no_trimbo == 0) {
n387=          %multiplier_str = %multiplier_str TRIMBO
n388=        }
n389=        else {
n390=          %multiplier_str = %multiplier_str COMBO
n391=          %multiplier = 2
n392=        }
n393=      }
n394=    }
n395=    if (%multiplier_str != $null) {
n396=      %multiplier_str = $chr(32) $+ - $+ $chr(32) $+ %multiplier_str
n397=    }
n398=
n399=    %points = %points * %multiplier
n400=    hadd question_points %player %points
n401=
n402=    var %point_description = %points $+ %multiplier_str
n403=    if (%points > 0) {
n404=      %point_description = + $+ %point_description
n405=    }
n406=    var %description = $hget(question_points, %i).item ( $+ %point_description $+ )
n407=    hadd point_descriptions %player %description
n408=
n409=    var %total_player_points 0
n410=    if ($hget(total_points, $hget(question_points, %i).item)) {
n411=      %total_player_points = $hget(total_points, $hget(question_points, %i).item) + %points
n412=    }
n413=    else {
n414=      inc %total_player_points %points
n415=    }
n416=    if (%ml_points_for_answers == 1) {
n417=      inc %total_player_points
n418=    }
n419=    hadd -m total_points $hget(question_points, %i).item %total_player_points
n420=
n421=    inc %i
n422=  }
n423=
n424=  var %winners_str
n425=  var %losers_str
n426=  hmake players_found
n427=  %i = 1
n428=  while ($hget(question_points, %i).item) {
n429=    var %max = -999999
n430=    var %min = 999999
n431=    var %max_player
n432=    var %min_player
n433=
n434=    var %j = 1
n435=    while ($hget(question_points, %j).item) {
n436=      if ($hget(question_points, %j).data > %max && !$hget(players_found, $hget(question_points, %j).item)) {
n437=        %max = $hget(question_points, %j).data
n438=        %max_player = $hget(question_points, %j).item
n439=      }
n440=      if ($hget(question_points, %j).data < %min && !$hget(players_found, $hget(question_points, %j).item)) {
n441=        %min = $hget(question_points, %j).data
n442=        %min_player = $hget(question_points, %j).item
n443=      }
n444=      inc %j
n445=    }
n446=
n447=    if (%max < 0 && %min > 0) {
n448=      break
n449=    }
n450=
n451=    if (%max > 0) {
n452=      if (%winners_str != $null) {
n453=        %winners_str = %winners_str $+ , $+ $chr(32)
n454=      }
n455=      %winners_str = %winners_str $hget(point_descriptions, %max_player)
n456=      hadd players_found %max_player 1
n457=    }
n458=    if (%min < 0) {
n459=      if (%losers_str != $null) {
n460=        %losers_str = %losers_str $+ , $+ $chr(32)
n461=      }
n462=      %losers_str = %losers_str $hget(point_descriptions, %min_player)
n463=      hadd players_found %min_player 1
n464=    }
n465=
n466=    inc %i
n467=  }
n468=  hfree players_found
n469=  hfree point_descriptions
n470=
n471=  ; aktualizacja mnoznikow
n472=  %i = 1
n473=  while ($hget(multipliers, %i).item) {
n474=    var %player = $hget(multipliers, %i).item
n475=    if (!$hget(question_points, %player) || $hget(question_points, %player) <= 0) {
n476=      hadd multipliers %player 1
n477=    }
n478=    inc %i
n479=  }
n480=
n481=  %i = 1
n482=  while ($hget(question_points, %i).item) {
n483=    var %player = $hget(question_points, %i).item
n484=    if ($hget(question_points, %player) > 0) {
n485=      var %multiplier 1
n486=      if (!$hget(multipliers, %player)) {
n487=        %multiplier = 2
n488=      }
n489=      else {
n490=        %multiplier = $hget(multipliers, %player)
n491=        if (%multiplier < 3) {
n492=          inc %multiplier
n493=        }
n494=      }
n495=      hadd -m multipliers %player %multiplier
n496=    }
n497=    inc %i
n498=  }
n499=
n500=  msg %ml_quiz_channel 9,1Prawidlowa odpowiedz podali:11,2 %winners_str
n501=  msg %ml_quiz_channel 4,1Punkty traca:4,2 %losers_str
n502=  unset %ml_round_*
n503=  if ($hget(question_points)) {
n504=    hfree question_points
n505=  }
n506=
n507=  var %nextdelay = %ml_question_delay
n508=  if ((5 // %ml_question_number) && (%ml_question_number < %ml_number_of_questions)) {
n509=    var %statdelay = %ml_question_delay / 2
n510=    .timerStats 1 %statdelay mstat 4
n511=    inc %nextdelay 5
n512=  }
n513=
n514=  if (%ml_running == 1) {
n515=    if (%ml_question_number == %ml_number_of_questions) {
n516=      .timerNext 1 3 end_of_questions
n517=    }
n518=    else {
n519=      .timerNext 1 %nextdelay next_question
n520=    }
n521=  }
n522=}
n523=
n524=end_of_questions {
n525=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Koniec pytan 2,2...................................................
n526=  halt
n527=}
n528=
n529=/mstat {
n530=  var %number_of_places = 99
n531=  if ($1 != $null) {
n532=    %number_of_places = $1
n533=  }
n534=
n535=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n536=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Punktacja po 0,1 %ml_question_number $+ . 08,02 rundach (0,1 %quiz_duration 8,2)2,2 $&
n537=    $adjust_length(3, %ml_question_number) $+ $adjust_length(31, %quiz_duration)
n538=
n539=  var %i = 1
n540=  var %prev_place = 1
n541=  var %prev_points = 10000
n542=  hmake found_stats
n543=  while ($hget(total_points, %i).item && %i <= %number_of_places) {
n544=    var %max = -100000
n545=    var %j = 1
n546=    var %max_player
n547=
n548=    while ($hget(total_points, %j).item) {
n549=      if ($hget(total_points, %j).data > %max && !$hget(found_stats, $hget(total_points, %j).item)) {
n550=        %max = $hget(total_points, %j).data
n551=        %max_player = $hget(total_points, %j).item
n552=      }
n553=      inc %j
n554=    }
n555=
n556=    hadd found_stats %max_player 1
n557=
n558=    var %place
n559=    if (%max == %prev_points) {
n560=      %place = %prev_place
n561=    }
n562=    else {
n563=      %place = %i
n564=    }
n565=
n566=    var %player_correct_answers = 0
n567=    var %player_incorrect_answers = 0
n568=    var %player_answers = 0
n569=    if ($hget(correct_answers, %max_player)) {
n570=      %player_correct_answers = $hget(correct_answers, %max_player)
n571=    }
n572=    if ($hget(incorrect_answers, %max_player)) {
n573=      %player_incorrect_answers = $hget(incorrect_answers, %max_player)
n574=    }
n575=    %player_answers = %player_correct_answers + %player_incorrect_answers
n576=    var %line = 2,2 $+ $adjust_length(3, %place) $+ 8,2 $+ $chr(32) $+ %place $+ . $&
n577=      Miejsce: 0,2 $+ %max_player $+ 2,2 $+ $adjust_length(26, %max_player) $+ 0,2 (2 $+ $adjust_length(3, %max)
n578=    if (%max > 0) {
n579=      %line = %line $+ 9 %max
n580=    }
n581=    elseif (%max < 0) {
n582=      %line = %line $+ 4 %max
n583=    }
n584=    else {
n585=      %line = %line $+ 11 %max
n586=    }
n587=    %line = %line $+ 0 $get_point_word(%max) $+ 2 $+ $adjust_length(7, $get_point_word(%max)) $+ 0), $&
n588=      odp2 $adjust_length(3, %player_answers) $+ 11 $+ $eval(%player_answers) $+ 0:2 $+ $adjust_length(3, %player_correct_answers) $+ 9 $&
n589=      $chr(32) $+ %player_correct_answers $+ 0 $+ $chr(32) $+ / $+ 2 $+ $adjust_length(3, %player_incorrect_answers) $+ 4 $+ $chr(32) $+ %player_incorrect_answers
n590=    msg %ml_quiz_channel %line $+ 2,2 ...... 
n591=
n592=    %prev_points = %max
n593=    %prev_place = %place
n594=    inc %i
n595=  }
n596=
n597=  hfree found_stats
n598=}
n599=
n600=/mkom {
n601=  window -c @Milioner
n602=  window -l @Milioner 0 0 580 440 courier new
n603=  aline @Milioner      8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz - Spis komend ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n604=  aline @Milioner 4/mon <#kanal> 6- wlaczenie quizu
n605=  aline @Milioner 4/mtryb [normal|punkt] 6- ustawienie trybu, punkt - dodawanie punktu za kazda udzielona odpowiedz (domyslnie: normal)
n606=  aline @Milioner 4/mtryb [pub|priv] 6- ustawienie trybu, pub - odpowiedzi na kanale i prywatnie, priv - tylko odpowiedzi prywatne (domyslnie)
n607=  aline @Milioner 4/mtryb [1234|abcd] 6- ustawienie trybu, odpowiedzi typu A/B/C/D (domyslnie) lub 1/2/3/4, ale mozna podac dowolne 4 rozne od siebie znaki
n608=  aline @Milioner 4/mtryb [mnoznik] 6- wlaczenie/wylaczenie naliczania mnoznika za serie odpowiedzi
n609=  aline @Milioner 4/mtryb [trimbo] 6- wlaczenie/wylaczenie najwyzszego mnoznika x3 (TRIMBO)
n610=  aline @Milioner 4/mplik <nazwapliku> 6- ustawienie pliku z pytaniami oraz uruchomienie quizu
n611=  aline @Milioner 4/mzasady 6- wyswietlenie informacji o zasadach gry
n612=  aline @Milioner 4/mpodp <podpowiedz> 6- podanie podpowiedzi
n613=  aline @Milioner 4/mstat [<liczba_miejsc>] 6- wyswietlenie wynikow quizu dla podanej liczby miejsc (jest nie podana - wyswietla pelna liste)
n614=  aline @Milioner 4/mwersja 6- informacja o wersji skryptu
n615=  aline @Milioner 4/mczas <czas_w_sekundach> 6- ustawienie czasu na odpowiedz
n616=  aline @Milioner 4/minterwal <czas_w_sekundach> 6- ustawienie opoznienia miedzy odpowiedzia a nastepnym pytaniem
n617=  aline @Milioner 4/mkanal <#kanal> 6- przeniesienie quizu na inny kanal
n618=  aline @Milioner 4/mpunkt <nick> [<ilosc_punktow>] 6- dodaje podanej osobie punkty (1 - jesli nie podano liczby)
n619=  aline @Milioner 4/mpauza 6- wstrzymanie/wznowienie quizu
n620=  aline @Milioner 4/mile <nick> 6- wyswietla ilosc punktow podanego gracza
n621=  aline @Milioner 4/mblokuj <nick> [<powod>] 6- uniemozliwia gre podanemu graczowi
n622=  aline @Milioner 4/modblokuj <nick> [<powod>] 6- przywraca gracza do gry
n623=  aline @Milioner 4/moff 6- wylaczenie quizu
n624=  aline @Milioner           8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n625=  aline @Milioner 6Plik z pytaniami do Quizu to zwykly plik txt. Musi on wygladac nastepujaco:
n626=  aline @Milioner Pytanie pierwsze
n627=  aline @Milioner Odpowiedz prawidlowa
n628=  aline @Milioner Odpowiedz bledna 1
n629=  aline @Milioner Odpowiedz bledna 2
n630=  aline @Milioner Odpowiedz bledna 3
n631=  aline @Milioner Pytanie drugie
n632=  aline @Milioner ...itp...
n633=  aline @Milioner 6Skrypt losowo wybiera literki przyporzadkowane poszczegolnym pytaniom.
n634=  aline @Milioner 6UWAGA: Przed rozpoczeciem Quizu Milionerowego nalezy zakonczyc wszystkie
n635=  aline @Milioner 6inne formy quizow (Redhead, Autoquiz itp.), a najlepiej wyladowac je z
n636=  aline @Milioner 6pamieci komenda /unload. Uniemozliwi to wykorzystanie przez pomylke
n637=  aline @Milioner 6komend z tych quizow, co spowodowaloby bledy w aktualnie prowadzonym
n638=  aline @Milioner 6Quizie Milionerowym.
n639=  aline @Milioner 6Jezeli macie inne pytania lub sugestie: 12http://www.quizpl.net
n640=}
n641=
n642=/mzasady {
n643=  msg %ml_quiz_channel 2,2 .8,2Punktowanie: pierwszy odpowiadajacy za dobra odpowiedz 11,2+3 pkt8,2, $&
n644=    za zla 4,2-3 pkt8,2, drugi odpowiadajacy odpowiednio 11,2+/4,2-2 pkt8,2, pozostali 11,2+/4,2-1 pkt8,2, $&
n645=    jesli poprawnie odpowiedziala tylko 1 osoba: BONUS 11,2+1 pkt8,2. 
n646=  if (%ml_no_multiplier == 0) {
n647=    if (%ml_no_trimbo == 0) {
n648=      msg %ml_quiz_channel 2,2 .8,2Poprawna odpowiedz oznacza, iz w nastepnym pytaniu punkty mnozone sa 11,2razy 2 (COMBO)8,2, $&
n649=        dwie i wiecej poprawnych odpowiedzi z rzedu powoduje mnozenie punktow w nastepnych pytaniach 11,2razy 3 (TRIMBO)8,2. $&
n650=        Uwaga! Mnozone sa punkty tak dodatnie jak i ujemne 4,2(!)8,2, a wiec mozna za pytanie zdobyc11,2 12 pkt8,2 $&
n651=        lub stracic4,2 9 pkt8,2; niepoprawna odpowiedz lub brak odpowiedzi kasuje mnoznik. 
n652=    }
n653=    else {
n654=      msg %ml_quiz_channel 2,2 .8,2Poprawna odpowiedz oznacza, iz w kazdym nastepnym pytaniu punkty mnozone sa 11,2razy 2 (COMBO)8,2. $&
n655=        Uwaga! Mnozone sa punkty tak dodatnie jak i ujemne 4,2(!)8,2, a wiec mozna za pytanie zdobyc11,2 8 pkt8,2 $&
n656=        lub stracic4,2 6 pkt8,2; niepoprawna odpowiedz lub brak odpowiedzi kasuje mnoznik. 
n657=    }
n658=  }
n659=  if (%ml_points_for_answers == 1) {
n660=    msg %ml_quiz_channel 2,2 .8,2Tryb punkt za odpowiedz oznacza, iz za kazda udzielona odpowiedz, bez wzgledu na to czy poprawna $&
n661=      czy bledna, gracz uzyskuje dodatkowo 11,2+1 pkt8,2. 
n662=  }
n663=}
n664=
n665=/mkanal {
n666=  set %ml_quiz_channel $1
n667=  echo 9,1 MILIONEROWY 08Quiz:8,2 Quiz przeniesiony na kanal 0,1 %ml_quiz_channel 2,2 $adjust_length(34, %ml_quiz_channel)
n668=}
n669=
n670=; player, points
n671=/mpunkt {
n672=  var %points 1
n673=  if ($2 != $null) {
n674=    %points = $2
n675=  }
n676=
n677=  var %total_player_points %points
n678=  if ($hget(total_points, $1)) {
n679=    inc %total_player_points $hget(total_points, $1)
n680=  }
n681=  hadd -m total_points $1 %total_player_points
n682=
n683=  echo 9,1 MILIONEROWY 08Quiz:8,2 Dodalem 0,1 $1 8,2 %points $get_point_word(%points) 02,02 $&
n684=    $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(40, $1)
n685=}
n686=
n687=; czyje, publicznie (1/0)
n688=/mile {
n689=  if ($hget(total_points, $1)) {
n690=    var %points = $hget(total_points, $1)
n691=    var %better_players = 0
n692=    var %i = 1
n693=    while ($hget(total_points, %i).item) {
n694=      if ($hget(total_points, %i).data > %points) {
n695=        inc %better_players
n696=      }
n697=      inc %i
n698=    }
n699=
n700=    var %place = %better_players + 1
n701=    if ($2 == 1) {
n702=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jest na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n703=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(25, $1)
n704=    }
n705=    else {
n706=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jestes na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n707=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(23, $1)
n708=    }
n709=  }
n710=  else {
n711=    if ($2 == 1) {
n712=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie ma jeszcze zadnego punktu2,2 $adjust_length(31, $1)
n713=    }
n714=    else {
n715=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie masz jeszcze zadnego punktu2,2 $adjust_length(29, $1)
n716=    }
n717=  }
n718=}
n719=
n720=/mpauza {
n721=  if (%ml_number_of_questions != $null) {
n722=    if (%ml_running == 1) {
n723=      set %ml_running 0
n724=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wstrzymana 2,2.................................................
n725=    }
n726=    else {
n727=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wznowiona 2,2..................................................
n728=      set %ml_running 1
n729=      next_question
n730=    }
n731=  }
n732=}
n733=
n734=//mwersja {
n735=  msg %ml_quiz_channel 1,1................9,1 MILIONEROWY 8Quiz (wersja %ml_version $+ ) by 0KrzS&Dizzy&wilk 1,1.................
n736=  msg %ml_quiz_channel 2,2.......................8,2 Sciagaj z: 0http://www.quizpl.net2,2 ..........................
n737=}
n738=
n739=/mczas {
n740=  set %ml_answer_time $1
n741=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas na odpowiedz: 0,1 %ml_answer_time 8,2 sekund(y)2,2 $adjust_length(3, %ml_answer_time) $+ .............................
n742=  zapiszconf
n743=}
n744=
n745=/minterwal {
n746=  set %ml_question_delay $1 
n747=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas pomiedzy pytaniami: 0,1 %ml_question_delay 8,2 sekund(y)2,2 $adjust_length(3, %ml_question_delay) $+ .......................
n748=  zapiszconf
n749=}
n750=
n751=/mdelay minterwal $1-
n752=
n753=/mpodp {
n754=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Podpowiedz: 0,1 $1- 2,2 $adjust_length(49, $1-)
n755=}
n756=
n757=//mblokuj {
n758=  hadd -m locked_players $1 1
n759=  var %reason $2-
n760=  if (%reason == $null) {
n761=    %reason = nie podano
n762=  }
n763=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Odpadasz z gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n764=}
n765=
n766=//modblokuj {
n767=  hdel locked_players $1
n768=  var %reason $2-
n769=  if (%reason == $null) {
n770=    %reason = nie podano
n771=  }
n772=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Wracasz do gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n773=}
n774=
n775=/wczytajconf {
n776=  var %tmp = $readini(milioner_conf.ini, milioner, PunktZaOdpowiedz)
n777=  if (%tmp isnum) {
n778=    if (%tmp > 0) set %ml_points_for_answers 1
n779=    else set %ml_points_for_answers 0
n780=  }
n781=  %tmp = $readini(milioner_conf.ini, milioner, OdpowiedziPubliczne)
n782=  if (%tmp isnum) {
n783=    if (%tmp > 0) set %ml_public 1
n784=    else set %ml_public 0
n785=  }
n786=  %tmp = $readini(milioner_conf.ini, milioner, BezMnoznika)
n787=  if (%tmp isnum) {
n788=    if (%tmp > 0) set %ml_no_multiplier 1
n789=    else set %ml_no_multiplier 0
n790=  }
n791=  %tmp = $readini(milioner_conf.ini, milioner, BezTrimbo)
n792=  if (%tmp isnum) {
n793=    if (%tmp > 0) set %ml_no_trimbo 1
n794=    else set %ml_no_trimbo 0
n795=  }
n796=  %tmp = $readini(milioner_conf.ini, milioner, CzasNaOdpowiedz)
n797=  if ((%tmp isnum) && (%tmp > 0)) set %ml_answer_time %tmp
n798=  %tmp = $readini(milioner_conf.ini, milioner, OpoznieniePytan)
n799=  if ((%tmp isnum) && (%tmp > 0)) set %ml_question_delay %tmp
n800=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaPierwszego)
n801=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_first %tmp
n802=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaDrugiego)
n803=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_second %tmp
n804=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaReszty)
n805=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_rest %tmp
n806=  %tmp = $readini(milioner_conf.ini, milioner, PunktyZaBonus)
n807=  if ((%tmp isnum) && (%tmp >= 0)) set %ml_points_bonus %tmp
n808=  %tmp = $readini(milioner_conf.ini, milioner, Symbole)
n809=  if ((%tmp != $null) && ($len(%tmp) == 4)) {
n810=    var %tmp_a = $upper($left(%tmp, 1))
n811=    var %tmp_b = $upper($mid(%tmp, 2, 1))
n812=    var %tmp_c = $upper($mid(%tmp, 3, 1))
n813=    var %tmp_d = $upper($right(%tmp, 1))
n814=    if ((%tmp_a != %tmp_b) && (%tmp_a != %tmp_c) && (%tmp_a != %tmp_d) && (%tmp_b != %tmp_c) && (%tmp_b != %tmp_d) && (%tmp_c != %tmp_d)) {
n815=      set %ml_a %tmp_a
n816=      set %ml_b %tmp_b
n817=      set %ml_c %tmp_c
n818=      set %ml_d %tmp_d
n819=    }
n820=  }
n821=}
n822=
n823=/zapiszconf {
n824=  writeini -n milioner_conf.ini milioner PunktZaOdpowiedz %ml_points_for_answers
n825=  writeini -n milioner_conf.ini milioner CzasNaOdpowiedz %ml_answer_time
n826=  writeini -n milioner_conf.ini milioner OpoznieniePytan %ml_question_delay
n827=  writeini -n milioner_conf.ini milioner OdpowiedziPubliczne %ml_public
n828=  writeini -n milioner_conf.ini milioner Symbole %ml_a $+ %ml_b $+ %ml_c $+ %ml_d
n829=  writeini -n milioner_conf.ini milioner BezMnoznika %ml_no_multiplier
n830=  writeini -n milioner_conf.ini milioner BezTrimbo %ml_no_trimbo
n831=  writeini -n milioner_conf.ini milioner PunktyDlaPierwszego %ml_points_first
n832=  writeini -n milioner_conf.ini milioner PunktyDlaDrugiego %ml_points_second
n833=  writeini -n milioner_conf.ini milioner PunktyDlaReszty %ml_points_rest
n834=  writeini -n milioner_conf.ini milioner PunktyZaBonus %ml_points_bonus
n835=}
