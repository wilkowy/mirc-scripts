[aliases]
n0=; Milionerowy QuizÂ® by KrzS&Dizzy&wilk
n1=; Radze tu nic nie zmieniac
n2=; mod by wilk (odpowiadanie na # i/lub priv, rozbudowany /mtryb, tryb 1234 lub dowolnie wybrany, ponowne przemieszanie by uniknac serii odpowiedzi, alias /mdelay, wylaczenie mnoznika/trimba/bonusow/antygooglera, zapisywanie ustawien w pliku)
n3=
n4=//mon {
n5=  if ($1 == $null) {
n6=    echo -a 9,1 MILIONEROWY 8QUIZ Podaj nazwe kanalu, na ktorym chcesz grac, np:  "/mon #kanal"
n7=    halt
n8=  }
n9=  unset %ml_*
n10=
n11=  set %ml_version 3.4
n12=  set %ml_answer_time 20
n13=  set %ml_question_delay 20
n14=  set %ml_points_for_answers 1
n15=  set %ml_public 0
n16=  set %ml_a A
n17=  set %ml_b B
n18=  set %ml_c C
n19=  set %ml_d D
n20=  set %ml_no_multiplier 0
n21=  set %ml_no_trimbo 0
n22=  set %ml_points_first 3
n23=  set %ml_points_second 2
n24=  set %ml_points_rest 1
n25=  set %ml_points_bonus 1
n26=  set %ml_antigoogler 1
n27=  set %ml_reshuffle 1
n28=
n29=  set %ml_previous
n30=
n31=  set %ml_question_number 0
n32=
n33=  load -rs milioner2.ini
n34=  set %ml_quiz_channel $$1
n35=  if ($hget(total_points)) {
n36=    hfree total_points
n37=  }
n38=  if ($hget(multipliers)) {
n39=    hfree multipliers
n40=  }
n41=  if ($hget(correct_answers)) {
n42=    hfree correct_answers
n43=  }
n44=  if ($hget(incorrect_answers)) {
n45=    hfree incorrect_answers
n46=  }
n47=  if ($hget(locked_players)) {
n48=    hfree locked_players
n49=  }
n50=
n51=  wczytajconf
n52=
n53=  echo 4Milionerowy Quiz (wersja %ml_version $+ ) wystartowal ! 
n54=  echo 4Spis komend /mkom
n55=  msg %ml_quiz_channel 1,1 ............9MILIONEROWY8 Quiz 8,1(wersja %ml_version $+ ) wystartowal na 0 $+ %ml_quiz_channel 1............ $adjust_length(11, %ml_quiz_channel)
n56=  msg %ml_quiz_channel 2,2 ....8,2 !zasady - zasady,2,2..8,2!ile - miejsce,2,2..8,2punktacja automatycznie co 5 pytan  2......
n57=  if (%ml_public == 0) {
n58=    msg %ml_quiz_channel 2,2 ...........15,2 >>> odpowiedzi udzielamy 4PRYWATNIE15 puszczajacemu quiz <<< 2............
n59=  }
n60=  else {
n61=    msg %ml_quiz_channel 2,2 .....15,2 >>> odpowiedzi mozna udzielac takze prywatnie puszczajacemu quiz <<< 2.......
n62=  }
n63=  if ((%ml_a != A) || (%ml_b != B) || (%ml_c != C) || (%ml_d != D)) {
n64=    msg %ml_quiz_channel 2,2 .....................15,2 >>> symbole odpowiedzi to: %ml_a $+ / $+ %ml_b $+ / $+ %ml_c $+ / $+ %ml_d <<< 2.....................
n65=  }
n66=  if (%ml_points_for_answers == 1) {
n67=    msg %ml_quiz_channel 2,2 .................15,2 >>> tryb "punkt za odpowiedz" jest aktywny <<< 2.................
n68=  }
n69=
n70=  set %ml_start_time $ctime
n71=}
n72=
n73=//mtryb {
n74=  if ($1 == punkt) {
n75=    set %ml_points_for_answers 1
n76=    msg %ml_quiz_channel 2,2 ............................11,2 Tryb: punkt za odpowiedz 2............................
n77=  }
n78=  elseif ($1 == normal) {
n79=    set %ml_points_for_answers 0
n80=    msg %ml_quiz_channel 2,2 ............................11,2 Tryb: punktowanie zwykle 2............................
n81=  }
n82=  elseif ($1 == pub) {
n83=    set %ml_public 1
n84=    msg %ml_quiz_channel 2,2 .................11,2 Tryb: mozna odpowiadac na kanale lub prywatnie 2.................
n85=  }
n86=  elseif ($1 == priv) {
n87=    set %ml_public 0
n88=    msg %ml_quiz_channel 2,2 ........................11,2 Tryb: odpowiedzi TYLKO prywatnie 2........................
n89=  }
n90=  elseif ($1 == mnoznik) {
n91=    if (%ml_no_multiplier == 0) {
n92=      set %ml_no_multiplier 1
n93=      msg %ml_quiz_channel 2,2 ..................11,2 Tryb: bonusy za serie odpowiedzi - wylaczony 2..................
n94=    }
n95=    else {
n96=      set %ml_no_multiplier 0
n97=      msg %ml_quiz_channel 2,2 ...................11,2 Tryb: bonusy za serie odpowiedzi - aktywny 2...................
n98=    }
n99=  }
n100=  elseif ($1 == trimbo) {
n101=    if (%ml_no_trimbo == 0) {
n102=      set %ml_no_trimbo 1
n103=      msg %ml_quiz_channel 2,2 .....................11,2 Tryb: mnoznik x3 (TRIMBO) - wylaczony 2......................
n104=    }
n105=    else {
n106=      set %ml_no_trimbo 0
n107=      msg %ml_quiz_channel 2,2 ......................11,2 Tryb: mnoznik x3 (TRIMBO) - dostepny 2......................
n108=    }
n109=  }
n110=  elseif ($1 == antygoogler) {
n111=    if (%ml_antigoogler == 0) {
n112=      set %ml_antigoogler 1
n113=      msg %ml_quiz_channel 2,2 .........................11,2 Tryb: AntyGoogler - wlaczony 2...........................
n114=    }
n115=    else {
n116=      set %ml_antigoogler 0
n117=      msg %ml_quiz_channel 2,2 .........................11,2 Tryb: AntyGoogler - wylaczony 2..........................
n118=    }
n119=  }
n120=  elseif ($1 == mieszaj) {
n121=    if (%ml_reshuffle == 0) {
n122=      set %ml_reshuffle 1
n123=      msg %ml_quiz_channel 2,2 ....................11,2 Tryb: przemieszanie powtorzen - aktywny 2.....................
n124=    }
n125=    else {
n126=      set %ml_reshuffle 0
n127=      msg %ml_quiz_channel 2,2 ...................11,2 Tryb: przemieszanie powtorzen - wylaczony 2....................
n128=    }
n129=  }
n130=  elseif ($len($1) == 4) {
n131=    var %ml_tmp_1 = $upper($left($1, 1))
n132=    var %ml_tmp_2 = $upper($mid($1, 2, 1))
n133=    var %ml_tmp_3 = $upper($mid($1, 3, 1))
n134=    var %ml_tmp_4 = $upper($right($1, 1))
n135=    if ((%ml_tmp_1 != %ml_tmp_2) && (%ml_tmp_1 != %ml_tmp_3) && (%ml_tmp_1 != %ml_tmp_4) && (%ml_tmp_2 != %ml_tmp_3) && (%ml_tmp_2 != %ml_tmp_4) && (%ml_tmp_3 != %ml_tmp_4)) {
n136=      set %ml_a %ml_tmp_1
n137=      set %ml_b %ml_tmp_2
n138=      set %ml_c %ml_tmp_3
n139=      set %ml_d %ml_tmp_4
n140=      msg %ml_quiz_channel 2,2 ............................11,2 Tryb: odpowiedzi %ml_a $+ / $+ %ml_b $+ / $+ %ml_c $+ / $+ %ml_d 2............................
n141=    }
n142=  }
n143=  zapiszconf
n144=}
n145=
n146=//moff {
n147=  .timerAnswers off
n148=  .timerWarning off
n149=  .timerResult off
n150=  .timerStats off
n151=  .timerNext off
n152=
n153=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n154=  msg %ml_quiz_channel 1,1 ...........9MILIONEROWY8 Quiz 8,1(wersja %ml_version by KrzS&Dizzy&wilk) zakonczony 1...........
n155=  if (%ml_question_number == 1) {
n156=    msg %ml_quiz_channel 2,2 ......................8,2 Czas gry: 0,1 %ml_question_number 8,2 runda w 0,1 %quiz_duration 2,2 $& 
n157=      $adjust_length(3, %ml_question_number) $+ $adjust_length(32, %quiz_duration)
n158=  }
n159=  else {
n160=    msg %ml_quiz_channel 2,2 ......................8,2 Czas gry: 0,1 %ml_question_number 8,2 rund w 0,1 %quiz_duration 2,2 $&
n161=      $adjust_length(3, %ml_question_number) $+ $adjust_length(33, %quiz_duration)
n162=  }
n163=  msg %ml_quiz_channel 2,2 .......................8,2 Sciagaj z: 0,2http://www.quizpl.net 2,2.........................
n164=  .unload -rs milioner2.ini
n165=  unset %ml_*
n166=  .rlevel =2
n167=}
n168=
n169=/mplik {
n170=  set %ml_file $1-
n171=  if ($exists(%ml_file) == $false) {
n172=    echo 9,1 MILIONEROWY 08Quiz:8,2 Plik 0,1 %ml_file 8,2 nie istnieje 2,2 $adjust_length(42, %ml_file)
n173=    halt
n174=  }
n175=
n176=  set %ml_number_of_questions $lines(%ml_file) / 5
n177=  set %ml_line_number 0 
n178=  echo 9,1 MILIONEROWY 08Quiz:8,2 Plik z pytaniami: 0,1 $nopath(%ml_file) 08,02, liczba pytan: 0,1 %ml_number_of_questions 2,2 $&
n179=    $adjust_length(22, $nopath(%ml_file)) $+ $adjust_length(3, %ml_number_of_questions)
n180=
n181=  if (%ml_question_delay == $null) {
n182=    set %ml_question_delay 10
n183=  }
n184=
n185=  set %ml_running 1
n186=  .timer 1 3 next_question
n187=}
n188=
n189=get_number_of_dots {
n190=  var %dot_length $len($2-)
n191=  dec %dot_length 7
n192=  %dot_length = $1 - %dot_length
n193=  return %dot_length 
n194=}
n195=
n196=add_dots {
n197=  var %dots = .
n198=  while ($len(%dots) < $get_number_of_dots($1,$2-)) {
n199=    %dots = %dots $+ . 
n200=  }
n201=  return %dots
n202=}
n203=
n204=; ile znakow, tekst
n205=adjust_length {
n206=  if ($len($2-) >= $1) {
n207=    return
n208=  }
n209=
n210=  var %dots = .
n211=  var %total_length = $len($2-) + $len(%dots)
n212=  while (%total_length < $1) {
n213=    %dots = %dots $+ .
n214=    inc %total_length
n215=  }
n216=
n217=  return %dots
n218=}
n219=
n220=antigoogle {
n221=  var %replacement =  $+ $2 $+ a08
n222=  return $replace($1,$chr(32),%replacement)
n223=}
n224=
n225=get_point_word {
n226=  if ($1 == $null) {
n227=    return
n228=  }
n229=  var %number = $abs($1)
n230=
n231=  if (%number == 1) {
n232=    return punkt
n233=  }
n234=  elseif ((%number > 10) && (%number < 20)) {
n235=    return punktow
n236=  }
n237=  elseif (($right(%number,1) > 1) && ($right(%number,1) < 5)) {
n238=    return punkty
n239=  }
n240=  else {
n241=    return punktow
n242=  }
n243=}
n244=
n245=read_line {
n246=  var %line $1
n247=  var %file_name $2-
n248=  var %command -l $+ %line 
n249=  return $read %command %file_name
n250=}
n251=
n252=next_question {
n253=  if (%ml_running == 0) {
n254=    return
n255=  }
n256=
n257=  inc %ml_line_number
n258=  var %current_question = $read_line(%ml_line_number, %ml_file)
n259=  if (%ml_line_number < $lines(%ml_file)) {
n260=
n261=    inc %ml_question_number
n262=
n263=    hmake answers
n264=    hmake symbols
n265=    var %i
n266=
n267=    ; reading answers
n268=    %i = 1
n269=    while (%i <= 4) {
n270=      inc %ml_line_number
n271=      hadd answers %i $read_line(%ml_line_number, %ml_file)
n272=      hadd symbols %i $chr($calc(96 + %i)) %ml_ [ $+ [ $chr($calc(96 + %i)) ] ]
n273=      inc %i
n274=    }
n275=
n276=    ; randomizing answers
n277=    %i = 4
n278=    while (%i > 0) {
n279=      var %j $rand(1, %i)
n280=      if (%i != %j) {
n281=        var %tmp $hget(symbols, %i)
n282=        hadd symbols %i $hget(symbols, %j)
n283=        hadd symbols %j %tmp
n284=      }
n285=      dec %i
n286=    }
n287=
n288=    ; rerandomizing answers if correct answer has the same symbol as in previous question ($null for first question)
n289=    if ((%ml_reshuffle == 1) && (%ml_previous != $null) && (%ml_previous == $right($hget(symbols, 1), 1))) {
n290=      %i = 4
n291=      while (%i > 0) {
n292=        var %j $rand(1, %i)
n293=        if (%i != %j) {
n294=          var %tmp $hget(symbols, %i)
n295=          hadd symbols %i $hget(symbols, %j)
n296=          hadd symbols %j %tmp
n297=        }
n298=        dec %i
n299=      }
n300=    }
n301=
n302=    set %ml_round_question %current_question
n303=
n304=    set %ml_round_correct_answer $right($hget(symbols, 1), 1)
n305=    set %ml_round_correct_answer_description $hget(answers, 1)
n306=
n307=    set %ml_previous %ml_round_correct_answer
n308=
n309=    %i = 1
n310=    while (%i <= 4) {
n311=      set %ml_round_answer_ [ $+ [ $left($hget(symbols, %i), 1) ] ] $+($chr(3), 08, $right($hget(symbols, %i), 1), :, $chr(32), $chr(3), 08, $hget(answers, %i))
n312=      inc %i
n313=    }
n314=
n315=    $display_question
n316=
n317=    hfree symbols
n318=    hfree answers
n319=  }
n320=  else {
n321=    end_of_questions
n322=  }
n323=}
n324=
n325=display_question {
n326=  if (%ml_antigoogler == 1) {
n327=    msg %ml_quiz_channel 8,1Pytanie $+ $chr(32) $+ %ml_question_number $+ / $+ %ml_number_of_questions $+ :8,1 $antigoogle(%ml_round_question, 01) 1,1 $add_dots(75, %ml_round_question)
n328=  }
n329=  else {
n330=    msg %ml_quiz_channel 8,1Pytanie $+ $chr(32) $+ %ml_question_number $+ / $+ %ml_number_of_questions $+ :8,1 %ml_round_question 1,1 $add_dots(75, %ml_round_question)
n331=  }
n332=  .timerAnswers 1 3 display_answers
n333=}
n334=
n335=display_answers {
n336=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_a, 02) $+ 2,2 $add_dots(45, %ml_round_answer_a) 1,1.08,02 $antigoogle(%ml_round_answer_b, 02) $+ 2,2 $add_dots(45, %ml_round_answer_b)
n337=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_c, 02) $+ 2,2 $add_dots(45, %ml_round_answer_c) 1,1.08,02 $antigoogle(%ml_round_answer_d, 02) $+ 2,2 $add_dots(45, %ml_round_answer_d)
n338=  echo 4 Prawidlowa odpowiedz to: %ml_round_correct_answer - %ml_round_correct_answer_description
n339=  if (%ml_answer_time > 10) {
n340=    .timerWarning 1 $calc(%ml_answer_time - 10) display_warning
n341=  }
n342=  .timerResult 1 %ml_answer_time display_result
n343=  set %ml_round_running 1
n344=}
n345=
n346=display_warning {
n347=  msg %ml_quiz_channel 8,1Uwaga:8,2 zostalo 10 sekund
n348=}
n349=
n350=; nick, 1 poprawna
n351=//register_answer {
n352=  var %points
n353=  if (!%ml_round_answered_first) {
n354=    %points = %ml_points_first
n355=    set %ml_round_answered_first 1
n356=  }
n357=  elseif (!%ml_round_answered_second) {
n358=    %points = %ml_points_second
n359=    set %ml_round_answered_second 1
n360=  }
n361=  else {
n362=    %points = %ml_points_rest
n363=  }
n364=  if ($2 != 1) {
n365=    %points = -1 * %points
n366=    if (!$hget(incorrect_answers, $1)) {
n367=      hadd -m incorrect_answers $1 1
n368=    }
n369=    else {
n370=      var %ile = $hget(incorrect_answers, $1) + 1
n371=      hadd -m incorrect_answers $1 %ile
n372=    }
n373=  }
n374=  else {
n375=    if (!$hget(correct_answers, $1)) {
n376=      hadd -m correct_answers $1 1
n377=    }
n378=    else {
n379=      var %ile = $hget(correct_answers, $1) + 1
n380=      hadd -m correct_answers $1 %ile
n381=    }
n382=  }
n383=
n384=  hadd -m question_points $1 %points
n385=}
n386=
n387=; nick, answer
n388=//question_answered {
n389=  if (%ml_round_running == 1 && $1 ison %ml_quiz_channel && !$hget(question_points, $1) && !$hget(locked_players, $1)) {
n390=    if (%ml_round_correct_answer == $2) {
n391=      register_answer $nick 1
n392=    }
n393=    else {
n394=      register_answer $nick 0
n395=    }
n396=  }
n397=}
n398=
n399=//display_result {
n400=  set %ml_round_running 0
n401=  msg %ml_quiz_channel 8,1Koniec Czasu:8,2 Prawidlowa odpowiedz to:9,2 %ml_round_correct_answer - %ml_round_correct_answer_description
n402=  var %i
n403=  var %number_of_correct_answers = 0
n404=
n405=  %i = 1
n406=  while ($hget(question_points, %i).item) {
n407=    if ($hget(question_points, %i).data > 0) {
n408=      inc %number_of_correct_answers
n409=    }
n410=    inc %i
n411=  }
n412=
n413=  ; aktualizacja punktow
n414=  hmake point_descriptions
n415=  %i = 1
n416=  while ($hget(question_points, %i).item) {
n417=    var %player = $hget(question_points, %i).item
n418=    var %points = $hget(question_points, %i).data
n419=    var %multiplier 1
n420=    var %multiplier_str
n421=
n422=    if ((%points > 0) && (%number_of_correct_answers == 1) && (%ml_points_bonus > 0)) {
n423=      inc %points %ml_points_bonus
n424=      %multiplier_str = BONUS
n425=    }
n426=    if (($hget(multipliers, %player) > 1) && (%ml_no_multiplier == 0)) {
n427=      %multiplier = $hget(multipliers, %player)
n428=      if (%multiplier_str != $null) {
n429=        %multiplier_str = %multiplier_str $+ , $+ $chr(32)
n430=      }
n431=      if (%multiplier == 2) {
n432=        %multiplier_str = %multiplier_str COMBO
n433=      }
n434=      else {
n435=        if (%ml_no_trimbo == 0) {
n436=          %multiplier_str = %multiplier_str TRIMBO
n437=        }
n438=        else {
n439=          %multiplier_str = %multiplier_str COMBO
n440=          %multiplier = 2
n441=        }
n442=      }
n443=    }
n444=    if (%multiplier_str != $null) {
n445=      %multiplier_str = $chr(32) $+ - $+ $chr(32) $+ %multiplier_str
n446=    }
n447=
n448=    %points = %points * %multiplier
n449=    hadd question_points %player %points
n450=
n451=    var %point_description = %points $+ %multiplier_str
n452=    if (%points > 0) {
n453=      %point_description = + $+ %point_description
n454=    }
n455=    var %description = $hget(question_points, %i).item ( $+ %point_description $+ )
n456=    hadd point_descriptions %player %description
n457=
n458=    var %total_player_points 0
n459=    if ($hget(total_points, $hget(question_points, %i).item)) {
n460=      %total_player_points = $hget(total_points, $hget(question_points, %i).item) + %points
n461=    }
n462=    else {
n463=      inc %total_player_points %points
n464=    }
n465=    if (%ml_points_for_answers == 1) {
n466=      inc %total_player_points
n467=    }
n468=    hadd -m total_points $hget(question_points, %i).item %total_player_points
n469=
n470=    inc %i
n471=  }
n472=
n473=  var %winners_str
n474=  var %losers_str
n475=  hmake players_found
n476=  %i = 1
n477=  while ($hget(question_points, %i).item) {
n478=    var %max = -999999
n479=    var %min = 999999
n480=    var %max_player
n481=    var %min_player
n482=
n483=    var %j = 1
n484=    while ($hget(question_points, %j).item) {
n485=      if ($hget(question_points, %j).data > %max && !$hget(players_found, $hget(question_points, %j).item)) {
n486=        %max = $hget(question_points, %j).data
n487=        %max_player = $hget(question_points, %j).item
n488=      }
n489=      if ($hget(question_points, %j).data < %min && !$hget(players_found, $hget(question_points, %j).item)) {
n490=        %min = $hget(question_points, %j).data
n491=        %min_player = $hget(question_points, %j).item
n492=      }
n493=      inc %j
n494=    }
n495=
n496=    if (%max < 0 && %min > 0) {
n497=      break
n498=    }
n499=
n500=    if (%max > 0) {
n501=      if (%winners_str != $null) {
n502=        %winners_str = %winners_str $+ , $+ $chr(32)
n503=      }
n504=      %winners_str = %winners_str $hget(point_descriptions, %max_player)
n505=      hadd players_found %max_player 1
n506=    }
n507=    if (%min < 0) {
n508=      if (%losers_str != $null) {
n509=        %losers_str = %losers_str $+ , $+ $chr(32)
n510=      }
n511=      %losers_str = %losers_str $hget(point_descriptions, %min_player)
n512=      hadd players_found %min_player 1
n513=    }
n514=
n515=    inc %i
n516=  }
n517=  hfree players_found
n518=  hfree point_descriptions
n519=
n520=  ; aktualizacja mnoznikow
n521=  %i = 1
n522=  while ($hget(multipliers, %i).item) {
n523=    var %player = $hget(multipliers, %i).item
n524=    if (!$hget(question_points, %player) || $hget(question_points, %player) <= 0) {
n525=      hadd multipliers %player 1
n526=    }
n527=    inc %i
n528=  }
n529=
n530=  %i = 1
n531=  while ($hget(question_points, %i).item) {
n532=    var %player = $hget(question_points, %i).item
n533=    if ($hget(question_points, %player) > 0) {
n534=      var %multiplier 1
n535=      if (!$hget(multipliers, %player)) {
n536=        %multiplier = 2
n537=      }
n538=      else {
n539=        %multiplier = $hget(multipliers, %player)
n540=        if (%multiplier < 3) {
n541=          inc %multiplier
n542=        }
n543=      }
n544=      hadd -m multipliers %player %multiplier
n545=    }
n546=    inc %i
n547=  }
n548=
n549=  msg %ml_quiz_channel 9,1Prawidlowa odpowiedz podali:11,2 %winners_str
n550=  msg %ml_quiz_channel 4,1Punkty traca:4,2 %losers_str
n551=  unset %ml_round_*
n552=  if ($hget(question_points)) {
n553=    hfree question_points
n554=  }
n555=
n556=  var %nextdelay = %ml_question_delay
n557=  if ((5 // %ml_question_number) && (%ml_question_number < %ml_number_of_questions)) {
n558=    var %statdelay = %ml_question_delay / 2
n559=    .timerStats 1 %statdelay mstat 4
n560=    inc %nextdelay 5
n561=  }
n562=
n563=  if (%ml_running == 1) {
n564=    if (%ml_question_number == %ml_number_of_questions) {
n565=      .timerNext 1 3 end_of_questions
n566=    }
n567=    else {
n568=      .timerNext 1 %nextdelay next_question
n569=    }
n570=  }
n571=}
n572=
n573=end_of_questions {
n574=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Koniec pytan 2,2...................................................
n575=  halt
n576=}
n577=
n578=/mstat {
n579=  var %number_of_places = 99
n580=  if ($1 != $null) {
n581=    %number_of_places = $1
n582=  }
n583=
n584=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n585=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Punktacja po 0,1 %ml_question_number $+ . 08,02 rundach (0,1 %quiz_duration 8,2)2,2 $&
n586=    $adjust_length(3, %ml_question_number) $+ $adjust_length(31, %quiz_duration)
n587=
n588=  var %i = 1
n589=  var %prev_place = 1
n590=  var %prev_points = 10000
n591=  hmake found_stats
n592=  while ($hget(total_points, %i).item && %i <= %number_of_places) {
n593=    var %max = -100000
n594=    var %j = 1
n595=    var %max_player
n596=
n597=    while ($hget(total_points, %j).item) {
n598=      if ($hget(total_points, %j).data > %max && !$hget(found_stats, $hget(total_points, %j).item)) {
n599=        %max = $hget(total_points, %j).data
n600=        %max_player = $hget(total_points, %j).item
n601=      }
n602=      inc %j
n603=    }
n604=
n605=    hadd found_stats %max_player 1
n606=
n607=    var %place
n608=    if (%max == %prev_points) {
n609=      %place = %prev_place
n610=    }
n611=    else {
n612=      %place = %i
n613=    }
n614=
n615=    var %player_correct_answers = 0
n616=    var %player_incorrect_answers = 0
n617=    var %player_answers = 0
n618=    if ($hget(correct_answers, %max_player)) {
n619=      %player_correct_answers = $hget(correct_answers, %max_player)
n620=    }
n621=    if ($hget(incorrect_answers, %max_player)) {
n622=      %player_incorrect_answers = $hget(incorrect_answers, %max_player)
n623=    }
n624=    %player_answers = %player_correct_answers + %player_incorrect_answers
n625=    var %line = 2,2 $+ $adjust_length(3, %place) $+ 8,2 $+ $chr(32) $+ %place $+ . $&
n626=      Miejsce: 0,2 $+ %max_player $+ 2,2 $+ $adjust_length(26, %max_player) $+ 0,2 (2 $+ $adjust_length(3, %max)
n627=    if (%max > 0) {
n628=      %line = %line $+ 9 %max
n629=    }
n630=    elseif (%max < 0) {
n631=      %line = %line $+ 4 %max
n632=    }
n633=    else {
n634=      %line = %line $+ 11 %max
n635=    }
n636=    %line = %line $+ 0 $get_point_word(%max) $+ 2 $+ $adjust_length(7, $get_point_word(%max)) $+ 0), $&
n637=      odp2 $adjust_length(3, %player_answers) $+ 11 $+ $eval(%player_answers) $+ 0:2 $+ $adjust_length(3, %player_correct_answers) $+ 9 $&
n638=      $chr(32) $+ %player_correct_answers $+ 0 $+ $chr(32) $+ / $+ 2 $+ $adjust_length(3, %player_incorrect_answers) $+ 4 $+ $chr(32) $+ %player_incorrect_answers
n639=    msg %ml_quiz_channel %line $+ 2,2 ...... 
n640=
n641=    %prev_points = %max
n642=    %prev_place = %place
n643=    inc %i
n644=  }
n645=
n646=  hfree found_stats
n647=}
n648=
n649=/mkom {
n650=  window -c @Milioner
n651=  window -l @Milioner 0 0 580 440 courier new
n652=  aline @Milioner      8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz - Spis komend ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n653=  aline @Milioner 4/mon <#kanal> 6- wlaczenie quizu
n654=  aline @Milioner 4/mtryb [normal|punkt] 6- ustawienie trybu: punkt - dodawanie punktu za kazda udzielona odpowiedz (domyslnie), normal - bez bonusa
n655=  aline @Milioner 4/mtryb [pub|priv] 6- ustawienie trybu: pub - odpowiedzi na kanale i prywatnie, priv - tylko odpowiedzi prywatne (domyslnie)
n656=  aline @Milioner 4/mtryb [1234|abcd] 6- zmiana symboli/klawiszy przypisanych do odpowiedzi na A/B/C/D (domyslnie) lub 1/2/3/4; mozna podac dowolne 4 rozne od siebie znaki
n657=  aline @Milioner 4/mtryb [mnoznik] 6- wlaczenie/wylaczenie mnoznika za serie odpowiedzi (COMBO i TRIMBO)
n658=  aline @Milioner 4/mtryb [trimbo] 6- wlaczenie/wylaczenie najwyzszego mnoznika x3 (TRIMBO)
n659=  aline @Milioner 4/mtryb [antygoogler] 6- wlaczenie/wylaczenie antygooglera chroniacego pytania
n660=  aline @Milioner 4/mtryb [mieszaj] 6- wlaczenie/wylaczenie ponownego wyboru symboli odpowiedzi, by zredukowac powtorki
n661=  aline @Milioner 4/mplik <nazwapliku> 6- ustawienie pliku z pytaniami oraz uruchomienie quizu
n662=  aline @Milioner 4/mzasady 6- wyswietlenie informacji o zasadach gry
n663=  aline @Milioner 4/mpodp <podpowiedz> 6- podanie podpowiedzi
n664=  aline @Milioner 4/mstat [<liczba_miejsc>] 6- wyswietlenie wynikow quizu dla podanej liczby miejsc (jest nie podana - wyswietla pelna liste)
n665=  aline @Milioner 4/mwersja 6- informacja o wersji skryptu
n666=  aline @Milioner 4/mczas <czas_w_sekundach> 6- ustawienie czasu na odpowiedz
n667=  aline @Milioner 4/minterwal <czas_w_sekundach> 6- ustawienie opoznienia miedzy odpowiedzia a nastepnym pytaniem (alias: /mdelay)
n668=  aline @Milioner 4/mkanal <#kanal> 6- przeniesienie quizu na inny kanal
n669=  aline @Milioner 4/mpunkt <nick> [<ilosc_punktow>] 6- dodaje podanej osobie punkty (1 - jesli nie podano liczby)
n670=  aline @Milioner 4/mpauza 6- wstrzymanie/wznowienie quizu
n671=  aline @Milioner 4/mile <nick> 6- wyswietla liczbe punktow podanego gracza
n672=  aline @Milioner 4/mblokuj <nick> [<powod>] 6- uniemozliwia gre podanemu graczowi
n673=  aline @Milioner 4/modblokuj <nick> [<powod>] 6- przywraca gracza do gry
n674=  aline @Milioner 4/moff 6- wylaczenie quizu
n675=  aline @Milioner           8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n676=  aline @Milioner 6Plik z pytaniami do Quizu to zwykly plik tekstowy (rozszerzenie .txt). Musi on wygladac nastepujaco:
n677=  aline @Milioner Pytanie pierwsze
n678=  aline @Milioner Odpowiedz prawidlowa
n679=  aline @Milioner Odpowiedz bledna 1
n680=  aline @Milioner Odpowiedz bledna 2
n681=  aline @Milioner Odpowiedz bledna 3
n682=  aline @Milioner Pytanie drugie
n683=  aline @Milioner ...itd...
n684=  aline @Milioner 6Skrypt losowo wybiera literki przyporzadkowane poszczegolnym pytaniom.
n685=  aline @Milioner 6UWAGA: Przed rozpoczeciem Quizu Milionerowego nalezy zakonczyc wszystkie
n686=  aline @Milioner 6inne formy quizow (Redhead, Autoquiz itp.), a najlepiej wyladowac je z
n687=  aline @Milioner 6pamieci komenda /unload. Uniemozliwi to wykorzystanie przez pomylke
n688=  aline @Milioner 6komend z tych quizow, co spowodowaloby bledy w aktualnie prowadzonym
n689=  aline @Milioner 6Quizie Milionerowym.
n690=  aline @Milioner 6Jezeli macie inne pytania lub sugestie: 12http://www.quizpl.net
n691=}
n692=
n693=/mzasady {
n694=  msg %ml_quiz_channel 2,2 .8,2Punktowanie: pierwszy odpowiadajacy za dobra odpowiedz 11,2+3 pkt8,2, $&
n695=    za zla 4,2-3 pkt8,2, drugi odpowiadajacy odpowiednio 11,2+/4,2-2 pkt8,2, pozostali 11,2+/4,2-1 pkt8,2, $&
n696=    jesli poprawnie odpowiedziala tylko 1 osoba: BONUS 11,2+1 pkt8,2. 
n697=  if (%ml_no_multiplier == 0) {
n698=    if (%ml_no_trimbo == 0) {
n699=      msg %ml_quiz_channel 2,2 .8,2Poprawna odpowiedz oznacza, iz w nastepnym pytaniu punkty mnozone sa 11,2razy 2 (COMBO)8,2, $&
n700=        dwie i wiecej poprawnych odpowiedzi z rzedu powoduje mnozenie punktow w nastepnych pytaniach 11,2razy 3 (TRIMBO)8,2. $&
n701=        Uwaga! Mnozone sa punkty tak dodatnie jak i ujemne 4,2(!)8,2, a wiec mozna za pytanie zdobyc11,2 12 pkt8,2 $&
n702=        lub stracic4,2 9 pkt8,2; niepoprawna odpowiedz lub brak odpowiedzi kasuje mnoznik. 
n703=    }
n704=    else {
n705=      msg %ml_quiz_channel 2,2 .8,2Poprawna odpowiedz oznacza, iz w kazdym nastepnym pytaniu punkty mnozone sa 11,2razy 2 (COMBO)8,2. $&
n706=        Uwaga! Mnozone sa punkty tak dodatnie jak i ujemne 4,2(!)8,2, a wiec mozna za pytanie zdobyc11,2 8 pkt8,2 $&
n707=        lub stracic4,2 6 pkt8,2; niepoprawna odpowiedz lub brak odpowiedzi kasuje mnoznik. 
n708=    }
n709=  }
n710=  if (%ml_points_for_answers == 1) {
n711=    msg %ml_quiz_channel 2,2 .8,2Tryb punkt za odpowiedz oznacza, iz za kazda udzielona odpowiedz, bez wzgledu na to czy poprawna $&
n712=      czy bledna, gracz uzyskuje dodatkowo 11,2+1 pkt8,2. 
n713=  }
n714=}
n715=
n716=/mkanal {
n717=  set %ml_quiz_channel $1
n718=  echo 9,1 MILIONEROWY 08Quiz:8,2 Quiz przeniesiony na kanal 0,1 %ml_quiz_channel 2,2 $adjust_length(34, %ml_quiz_channel)
n719=}
n720=
n721=; player, points
n722=/mpunkt {
n723=  var %points 1
n724=  if ($2 != $null) {
n725=    %points = $2
n726=  }
n727=
n728=  var %total_player_points %points
n729=  if ($hget(total_points, $1)) {
n730=    inc %total_player_points $hget(total_points, $1)
n731=  }
n732=  hadd -m total_points $1 %total_player_points
n733=
n734=  echo 9,1 MILIONEROWY 08Quiz:8,2 Dodalem 0,1 $1 8,2 %points $get_point_word(%points) 02,02 $&
n735=    $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(40, $1)
n736=}
n737=
n738=; czyje, publicznie (1/0)
n739=/mile {
n740=  if ($hget(total_points, $1)) {
n741=    var %points = $hget(total_points, $1)
n742=    var %better_players = 0
n743=    var %i = 1
n744=    while ($hget(total_points, %i).item) {
n745=      if ($hget(total_points, %i).data > %points) {
n746=        inc %better_players
n747=      }
n748=      inc %i
n749=    }
n750=
n751=    var %place = %better_players + 1
n752=    if ($2 == 1) {
n753=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jest na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n754=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(25, $1)
n755=    }
n756=    else {
n757=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jestes na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n758=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(23, $1)
n759=    }
n760=  }
n761=  else {
n762=    if ($2 == 1) {
n763=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie ma jeszcze zadnego punktu2,2 $adjust_length(31, $1)
n764=    }
n765=    else {
n766=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie masz jeszcze zadnego punktu2,2 $adjust_length(29, $1)
n767=    }
n768=  }
n769=}
n770=
n771=/mpauza {
n772=  if (%ml_number_of_questions != $null) {
n773=    if (%ml_running == 1) {
n774=      set %ml_running 0
n775=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wstrzymana 2,2.................................................
n776=    }
n777=    else {
n778=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wznowiona 2,2..................................................
n779=      set %ml_running 1
n780=      next_question
n781=    }
n782=  }
n783=}
n784=
n785=//mwersja {
n786=  msg %ml_quiz_channel 1,1................9,1 MILIONEROWY 8Quiz (wersja %ml_version $+ ) by 0KrzS&Dizzy&wilk 1,1.................
n787=  msg %ml_quiz_channel 2,2.......................8,2 Sciagaj z: 0http://www.quizpl.net2,2 ..........................
n788=}
n789=
n790=/mczas {
n791=  set %ml_answer_time $1
n792=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas na odpowiedz: 0,1 %ml_answer_time 8,2 sekund(y)2,2 $adjust_length(3, %ml_answer_time) $+ .............................
n793=  zapiszconf
n794=}
n795=
n796=/minterwal {
n797=  set %ml_question_delay $1 
n798=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas pomiedzy pytaniami: 0,1 %ml_question_delay 8,2 sekund(y)2,2 $adjust_length(3, %ml_question_delay) $+ .......................
n799=  zapiszconf
n800=}
n801=
n802=/mdelay minterwal $1-
n803=
n804=/mpodp {
n805=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Podpowiedz: 0,1 $1- 2,2 $adjust_length(49, $1-)
n806=}
n807=
n808=//mblokuj {
n809=  hadd -m locked_players $1 1
n810=  var %reason $2-
n811=  if (%reason == $null) {
n812=    %reason = nie podano
n813=  }
n814=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Odpadasz z gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n815=}
n816=
n817=//modblokuj {
n818=  hdel locked_players $1
n819=  var %reason $2-
n820=  if (%reason == $null) {
n821=    %reason = nie podano
n822=  }
n823=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Wracasz do gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n824=}
n825=
n826=/wczytajconf {
n827=  var %tmp = $readini(milioner_conf.ini, milioner, PunktZaOdpowiedz)
n828=  if (%tmp isnum) {
n829=    if (%tmp > 0) set %ml_points_for_answers 1
n830=    else set %ml_points_for_answers 0
n831=  }
n832=  %tmp = $readini(milioner_conf.ini, milioner, OdpowiedziPubliczne)
n833=  if (%tmp isnum) {
n834=    if (%tmp > 0) set %ml_public 1
n835=    else set %ml_public 0
n836=  }
n837=  %tmp = $readini(milioner_conf.ini, milioner, BezMnoznika)
n838=  if (%tmp isnum) {
n839=    if (%tmp > 0) set %ml_no_multiplier 1
n840=    else set %ml_no_multiplier 0
n841=  }
n842=  %tmp = $readini(milioner_conf.ini, milioner, BezTrimbo)
n843=  if (%tmp isnum) {
n844=    if (%tmp > 0) set %ml_no_trimbo 1
n845=    else set %ml_no_trimbo 0
n846=  }
n847=  %tmp = $readini(milioner_conf.ini, milioner, AntyGoogler)
n848=  if (%tmp isnum) {
n849=    if (%tmp > 0) set %ml_antigoogler 1
n850=    else set %ml_antigoogler 0
n851=  }
n852=  %tmp = $readini(milioner_conf.ini, milioner, Przemieszaj)
n853=  if (%tmp isnum) {
n854=    if (%tmp > 0) set %ml_reshuffle 1
n855=    else set %ml_reshuffle 0
n856=  }
n857=  %tmp = $readini(milioner_conf.ini, milioner, CzasNaOdpowiedz)
n858=  if ((%tmp isnum) && (%tmp > 0)) set %ml_answer_time %tmp
n859=  %tmp = $readini(milioner_conf.ini, milioner, OpoznieniePytan)
n860=  if ((%tmp isnum) && (%tmp > 0)) set %ml_question_delay %tmp
n861=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaPierwszego)
n862=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_first %tmp
n863=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaDrugiego)
n864=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_second %tmp
n865=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaReszty)
n866=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_rest %tmp
n867=  %tmp = $readini(milioner_conf.ini, milioner, PunktyZaBonus)
n868=  if ((%tmp isnum) && (%tmp >= 0)) set %ml_points_bonus %tmp
n869=  %tmp = $readini(milioner_conf.ini, milioner, Symbole)
n870=  if ((%tmp != $null) && ($len(%tmp) == 4)) {
n871=    var %tmp_a = $upper($left(%tmp, 1))
n872=    var %tmp_b = $upper($mid(%tmp, 2, 1))
n873=    var %tmp_c = $upper($mid(%tmp, 3, 1))
n874=    var %tmp_d = $upper($right(%tmp, 1))
n875=    if ((%tmp_a != %tmp_b) && (%tmp_a != %tmp_c) && (%tmp_a != %tmp_d) && (%tmp_b != %tmp_c) && (%tmp_b != %tmp_d) && (%tmp_c != %tmp_d)) {
n876=      set %ml_a %tmp_a
n877=      set %ml_b %tmp_b
n878=      set %ml_c %tmp_c
n879=      set %ml_d %tmp_d
n880=    }
n881=  }
n882=}
n883=
n884=/zapiszconf {
n885=  writeini -n milioner_conf.ini milioner PunktZaOdpowiedz %ml_points_for_answers
n886=  writeini -n milioner_conf.ini milioner CzasNaOdpowiedz %ml_answer_time
n887=  writeini -n milioner_conf.ini milioner OpoznieniePytan %ml_question_delay
n888=  writeini -n milioner_conf.ini milioner OdpowiedziPubliczne %ml_public
n889=  writeini -n milioner_conf.ini milioner Symbole %ml_a $+ %ml_b $+ %ml_c $+ %ml_d
n890=  writeini -n milioner_conf.ini milioner BezMnoznika %ml_no_multiplier
n891=  writeini -n milioner_conf.ini milioner BezTrimbo %ml_no_trimbo
n892=  writeini -n milioner_conf.ini milioner AntyGoogler %ml_antigoogler
n893=  writeini -n milioner_conf.ini milioner Przemieszaj %ml_reshuffle
n894=  writeini -n milioner_conf.ini milioner PunktyDlaPierwszego %ml_points_first
n895=  writeini -n milioner_conf.ini milioner PunktyDlaDrugiego %ml_points_second
n896=  writeini -n milioner_conf.ini milioner PunktyDlaReszty %ml_points_rest
n897=  writeini -n milioner_conf.ini milioner PunktyZaBonus %ml_points_bonus
n898=}
