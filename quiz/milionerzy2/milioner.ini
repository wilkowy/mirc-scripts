[aliases]
n0=; Milionerowy QuizÂ® by KrzS&Dizzy
n1=; Radze tu nic nie zmieniac
n2=; mod by wilk (odpowiadanie na # i/lub priv, rozbudowany /mtryb, tryb 1234 lub dowolnie wybrany, alias /mdelay, wylaczenie mnoznika/trimba/bonusow/antygooglera, zapisywanie ustawien w pliku)
n3=
n4=//mon {
n5=  if ($1 == $null) {
n6=    echo -a 9,1 MILIONEROWY 8QUIZ Podaj nazwe kanalu na ktorym chcesz grac, np:  "/mon #kanal"
n7=    halt
n8=  }
n9=  unset %ml_*
n10=
n11=  set %ml_version 3.3
n12=  set %ml_answer_time 20
n13=  set %ml_question_delay 20
n14=  set %ml_question_number 0
n15=  set %ml_points_for_answers 1
n16=  set %ml_public 0
n17=  set %ml_a A
n18=  set %ml_b B
n19=  set %ml_c C
n20=  set %ml_d D
n21=  set %ml_no_multiplier 0
n22=  set %ml_no_trimbo 0
n23=  set %ml_points_first 3
n24=  set %ml_points_second 2
n25=  set %ml_points_rest 1
n26=  set %ml_points_bonus 1
n27=  set %ml_antigoogler 1
n28=
n29=  load -rs milioner2.ini
n30=  set %ml_quiz_channel $$1
n31=  if ($hget(total_points)) {
n32=    hfree total_points
n33=  }
n34=  if ($hget(multipliers)) {
n35=    hfree multipliers
n36=  }
n37=  if ($hget(correct_answers)) {
n38=    hfree correct_answers
n39=  }
n40=  if ($hget(incorrect_answers)) {
n41=    hfree incorrect_answers
n42=  }
n43=  if ($hget(locked_players)) {
n44=    hfree locked_players
n45=  }
n46=
n47=  wczytajconf
n48=
n49=  echo 4Milionerowy Quiz (wersja %ml_version $+ ) wystartowal ! 
n50=  echo 4Spis komend /mkom
n51=  msg %ml_quiz_channel 1,1 ............9MILIONEROWY8 Quiz 8,1(wersja %ml_version $+ ) wystartowal na 0 $+ %ml_quiz_channel 1............ $adjust_length(11, %ml_quiz_channel)
n52=  msg %ml_quiz_channel 2,2 ....8,2 !zasady - zasady,2,2..8,2!ile - miejsce,2,2..8,2punktacja automatycznie co 5 pytan  2......
n53=  if (%ml_public == 0) {
n54=    msg %ml_quiz_channel 2,2 ...........15,2 >>> odpowiedzi udzielamy PRYWATNIE puszczajacemu quiz <<< 2............
n55=  }
n56=  else {
n57=    msg %ml_quiz_channel 2,2 .....15,2 >>> odpowiedzi mozna udzielac takze prywatnie puszczajacemu quiz <<< 2.......
n58=  }
n59=  if ((%ml_a != A) || (%ml_b != B) || (%ml_c != C) || (%ml_d != D)) {
n60=    msg %ml_quiz_channel 2,2 .....................15,2 >>> symbole odpowiedzi to: %ml_a $+ / $+ %ml_b $+ / $+ %ml_c $+ / $+ %ml_d <<< 2.....................
n61=  }
n62=  if (%ml_points_for_answers == 1) {
n63=    msg %ml_quiz_channel 2,2 .................15,2 >>> tryb "punkt za odpowiedz" jest aktywny <<< 2.................
n64=  }
n65=
n66=  set %ml_start_time $ctime
n67=}
n68=
n69=//mtryb {
n70=  if ($1 == punkt) {
n71=    set %ml_points_for_answers 1
n72=    msg %ml_quiz_channel 2,2 ............................11,2 Tryb: punkt za odpowiedz 2............................
n73=  }
n74=  elseif ($1 == normal) {
n75=    set %ml_points_for_answers 0
n76=    msg %ml_quiz_channel 2,2 ............................11,2 Tryb: punktowanie zwykle 2............................
n77=  }
n78=  elseif ($1 == pub) {
n79=    set %ml_public 1
n80=    msg %ml_quiz_channel 2,2 .................11,2 Tryb: mozna odpowiadac na kanale lub prywatnie 2.................
n81=  }
n82=  elseif ($1 == priv) {
n83=    set %ml_public 0
n84=    msg %ml_quiz_channel 2,2 ........................11,2 Tryb: odpowiedzi TYLKO prywatnie 2........................
n85=  }
n86=  elseif ($1 == mnoznik) {
n87=    if (%ml_no_multiplier == 0) {
n88=      set %ml_no_multiplier 1
n89=      msg %ml_quiz_channel 2,2 ..................11,2 Tryb: bonusy za serie odpowiedzi - wylaczone 2..................
n90=    }
n91=    else {
n92=      set %ml_no_multiplier 0
n93=      msg %ml_quiz_channel 2,2 ...................11,2 Tryb: bonusy za serie odpowiedzi - aktywne 2...................
n94=    }
n95=  }
n96=  elseif ($1 == trimbo) {
n97=    if (%ml_no_trimbo == 0) {
n98=      set %ml_no_trimbo 1
n99=      msg %ml_quiz_channel 2,2 .....................11,2 Tryb: mnoznik x3 (TRIMBO) - wylaczony 2......................
n100=    }
n101=    else {
n102=      set %ml_no_trimbo 0
n103=      msg %ml_quiz_channel 2,2 ......................11,2 Tryb: mnoznik x3 (TRIMBO) - dostepny 2......................
n104=    }
n105=  }
n106=  elseif ($1 == antygoogler) {
n107=    if (%ml_antigoogler == 0) {
n108=      set %ml_antigoogler 1
n109=      msg %ml_quiz_channel 2,2 .........................11,2 Tryb: AntyGoogler - wlaczony 2...........................
n110=    }
n111=    else {
n112=      set %ml_antigoogler 0
n113=      msg %ml_quiz_channel 2,2 .........................11,2 Tryb: AntyGoogler - wylaczony 2..........................
n114=    }
n115=  }
n116=  elseif ($len($1) == 4) {
n117=    var %ml_tmp_1 = $upper($left($1, 1))
n118=    var %ml_tmp_2 = $upper($mid($1, 2, 1))
n119=    var %ml_tmp_3 = $upper($mid($1, 3, 1))
n120=    var %ml_tmp_4 = $upper($right($1, 1))
n121=    if ((%ml_tmp_1 != %ml_tmp_2) && (%ml_tmp_1 != %ml_tmp_3) && (%ml_tmp_1 != %ml_tmp_4) && (%ml_tmp_2 != %ml_tmp_3) && (%ml_tmp_2 != %ml_tmp_4) && (%ml_tmp_3 != %ml_tmp_4)) {
n122=      set %ml_a %ml_tmp_1
n123=      set %ml_b %ml_tmp_2
n124=      set %ml_c %ml_tmp_3
n125=      set %ml_d %ml_tmp_4
n126=      msg %ml_quiz_channel 2,2 ............................11,2 Tryb: odpowiedzi %ml_a $+ / $+ %ml_b $+ / $+ %ml_c $+ / $+ %ml_d 2............................
n127=    }
n128=  }
n129=  zapiszconf
n130=}
n131=
n132=//moff {
n133=  .timerAnswers off
n134=  .timerWarning off
n135=  .timerResult off
n136=  .timerStats off
n137=  .timerNext off
n138=
n139=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n140=  msg %ml_quiz_channel 1,1 ...........9MILIONEROWY8 Quiz 8,1(wersja %ml_version by KrzS&Dizzy&wilk) zakonczony 1...........
n141=  if (%ml_question_number == 1) {
n142=    msg %ml_quiz_channel 2,2 ......................8,2 Czas gry: 0,1 %ml_question_number 8,2 runda w 0,1 %quiz_duration 2,2 $& 
n143=      $adjust_length(3, %ml_question_number) $+ $adjust_length(32, %quiz_duration)
n144=  }
n145=  else {
n146=    msg %ml_quiz_channel 2,2 ......................8,2 Czas gry: 0,1 %ml_question_number 8,2 rund w 0,1 %quiz_duration 2,2 $&
n147=      $adjust_length(3, %ml_question_number) $+ $adjust_length(33, %quiz_duration)
n148=  }
n149=  msg %ml_quiz_channel 2,2 .......................8,2 Sciagaj z: 0,2http://www.quizpl.net 2,2.........................
n150=  .unload -rs milioner2.ini
n151=  unset %ml_*
n152=  .rlevel =2
n153=}
n154=
n155=/mplik {
n156=  set %ml_file $1-
n157=  if ($exists(%ml_file) == $false) {
n158=    echo 9,1 MILIONEROWY 08Quiz:8,2 Plik 0,1 %ml_file 8,2 nie istnieje 2,2 $adjust_length(42, %ml_file)
n159=    halt
n160=  }
n161=
n162=  set %ml_number_of_questions $lines(%ml_file) / 5
n163=  set %ml_line_number 0 
n164=  echo 9,1 MILIONEROWY 08Quiz:8,2 Plik z pytaniami: 0,1 $nopath(%ml_file) 08,02, liczba pytan: 0,1 %ml_number_of_questions 2,2 $&
n165=    $adjust_length(22, $nopath(%ml_file)) $+ $adjust_length(3, %ml_number_of_questions)
n166=
n167=  if (%ml_question_delay == $null) {
n168=    set %ml_question_delay 10
n169=  }
n170=
n171=  set %ml_running 1
n172=  .timer 1 3 next_question
n173=}
n174=
n175=get_number_of_dots {
n176=  var %dot_length $len($2-)
n177=  dec %dot_length 7
n178=  %dot_length = $1 - %dot_length
n179=  return %dot_length 
n180=}
n181=
n182=add_dots {
n183=  var %dots = .
n184=  while ($len(%dots) < $get_number_of_dots($1,$2-)) {
n185=    %dots = %dots $+ . 
n186=  }
n187=  return %dots
n188=}
n189=
n190=; ile znakow, tekst
n191=adjust_length {
n192=  if ($len($2-) >= $1) {
n193=    return
n194=  }
n195=
n196=  var %dots = .
n197=  var %total_length = $len($2-) + $len(%dots)
n198=  while (%total_length < $1) {
n199=    %dots = %dots $+ .
n200=    inc %total_length
n201=  }
n202=
n203=  return %dots
n204=}
n205=
n206=antigoogle {
n207=  var %replacement =  $+ $2 $+ a08
n208=  return $replace($1,$chr(32),%replacement)
n209=}
n210=
n211=get_point_word {
n212=  if ($1 == $null) {
n213=    return
n214=  }
n215=  var %number = $abs($1)
n216=
n217=  if (%number == 1) {
n218=    return punkt
n219=  }
n220=  elseif ((%number > 10) && (%number < 20)) {
n221=    return punktow
n222=  }
n223=  elseif (($right(%number,1) > 1) && ($right(%number,1) < 5)) {
n224=    return punkty
n225=  }
n226=  else {
n227=    return punktow
n228=  }
n229=}
n230=
n231=read_line {
n232=  var %line $1
n233=  var %file_name $2-
n234=  var %command -l $+ %line 
n235=  return $read %command %file_name
n236=}
n237=
n238=next_question {
n239=  if (%ml_running == 0) {
n240=    return
n241=  }
n242=
n243=  inc %ml_line_number
n244=  var %current_question = $read_line(%ml_line_number, %ml_file)
n245=  if (%ml_line_number < $lines(%ml_file)) {
n246=
n247=    hmake answers
n248=    hmake random_answers
n249=    var %i
n250=
n251=    ; reading answers
n252=    %i = 1
n253=    while (%i <= 4) {
n254=      inc %ml_line_number
n255=      hadd answers $chr($calc(64 + %i)) $read_line(%ml_line_number, %ml_file)
n256=      inc %i
n257=    }
n258=
n259=    ; randomizing answers
n260=    %i = 1
n261=    while (%i <= 4) {
n262=      var %answer_number = $rand(1, $calc(5 - %i))  
n263=      var %answer_key = $hget(answers, %answer_number).item
n264=      var %answer_description = $+($chr(3), 08, %ml_ [ $+ [ $chr($calc(96 + %i)) ] ], :, $chr(32), $chr(3), 08, $hget(answers, %answer_number).data)
n265=
n266=      hadd random_answers $chr($calc(64 + %i)) %answer_description      
n267=      if (%answer_number == 1 && %ml_round_correct_answer == $null) {
n268=        set %ml_round_correct_answer %ml_ [ $+ [ $chr($calc(96 + %i)) ] ]
n269=        set %ml_round_correct_answer_description $hget(answers, %answer_number).data
n270=      }
n271=      hdel answers %answer_key
n272=
n273=      inc %i
n274=    }
n275=
n276=    inc %ml_question_number
n277=    set %ml_round_question %current_question
n278=    set %ml_round_answer_a $hget(random_answers, A)
n279=    set %ml_round_answer_b $hget(random_answers, B)
n280=    set %ml_round_answer_c $hget(random_answers, C)
n281=    set %ml_round_answer_d $hget(random_answers, D)
n282=    $display_question
n283=
n284=    hfree random_answers
n285=    hfree answers
n286=  }
n287=  else {
n288=    end_of_questions
n289=  }
n290=}
n291=
n292=display_question {
n293=  if (%ml_antigoogler == 1) {
n294=    msg %ml_quiz_channel 8,1Pytanie $+ $chr(32) $+ %ml_question_number $+ / $+ %ml_number_of_questions $+ :8,1 $antigoogle(%ml_round_question, 01) 1,1 $add_dots(75, %ml_round_question)
n295=  }
n296=  else {
n297=    msg %ml_quiz_channel 8,1Pytanie $+ $chr(32) $+ %ml_question_number $+ / $+ %ml_number_of_questions $+ :8,1 %ml_round_question 1,1 $add_dots(75, %ml_round_question)
n298=  }
n299=  .timerAnswers 1 3 display_answers
n300=}
n301=
n302=display_answers {
n303=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_a, 02) $+ 2,2 $add_dots(45, %ml_round_answer_a) 1,1.08,02 $antigoogle(%ml_round_answer_b, 02) $+ 2,2 $add_dots(45, %ml_round_answer_b)
n304=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_c, 02) $+ 2,2 $add_dots(45, %ml_round_answer_c) 1,1.08,02 $antigoogle(%ml_round_answer_d, 02) $+ 2,2 $add_dots(45, %ml_round_answer_d)
n305=  echo 4 Prawidlowa odpowiedz to: %ml_round_correct_answer - %ml_round_correct_answer_description
n306=  if (%ml_answer_time > 10) {
n307=    .timerWarning 1 $calc(%ml_answer_time - 10) display_warning
n308=  }
n309=  .timerResult 1 %ml_answer_time display_result
n310=  set %ml_round_running 1
n311=}
n312=
n313=display_warning {
n314=  msg %ml_quiz_channel 8,1Uwaga:8,2 zostalo 10 sekund
n315=}
n316=
n317=; nick, 1 poprawna
n318=//register_answer {
n319=  var %points
n320=  if (!%ml_round_answered_first) {
n321=    %points = %ml_points_first
n322=    set %ml_round_answered_first 1
n323=  }
n324=  elseif (!%ml_round_answered_second) {
n325=    %points = %ml_points_second
n326=    set %ml_round_answered_second 1
n327=  }
n328=  else {
n329=    %points = %ml_points_rest
n330=  }
n331=  if ($2 != 1) {
n332=    %points = -1 * %points
n333=    if (!$hget(incorrect_answers, $1)) {
n334=      hadd -m incorrect_answers $1 1
n335=    }
n336=    else {
n337=      var %ile = $hget(incorrect_answers, $1) + 1
n338=      hadd -m incorrect_answers $1 %ile
n339=    }
n340=  }
n341=  else {
n342=    if (!$hget(correct_answers, $1)) {
n343=      hadd -m correct_answers $1 1
n344=    }
n345=    else {
n346=      var %ile = $hget(correct_answers, $1) + 1
n347=      hadd -m correct_answers $1 %ile
n348=    }
n349=  }
n350=
n351=  hadd -m question_points $1 %points
n352=}
n353=
n354=; nick, answer
n355=//question_answered {
n356=  if (%ml_round_running == 1 && $1 ison %ml_quiz_channel && !$hget(question_points, $1) && !$hget(locked_players, $1)) {
n357=    if (%ml_round_correct_answer == $2) {
n358=      register_answer $nick 1
n359=    }
n360=    else {
n361=      register_answer $nick 0
n362=    }
n363=  }
n364=}
n365=
n366=//display_result {
n367=  set %ml_round_running 0
n368=  msg %ml_quiz_channel 8,1Koniec Czasu:8,2 Prawidlowa odpowiedz to:9,2 %ml_round_correct_answer - %ml_round_correct_answer_description
n369=  var %i
n370=  var %number_of_correct_answers = 0
n371=
n372=  %i = 1
n373=  while ($hget(question_points, %i).item) {
n374=    if ($hget(question_points, %i).data > 0) {
n375=      inc %number_of_correct_answers
n376=    }
n377=    inc %i
n378=  }
n379=
n380=  ; aktualizacja punktow
n381=  hmake point_descriptions
n382=  %i = 1
n383=  while ($hget(question_points, %i).item) {
n384=    var %player = $hget(question_points, %i).item
n385=    var %points = $hget(question_points, %i).data
n386=    var %multiplier 1
n387=    var %multiplier_str
n388=
n389=    if ((%points > 0) && (%number_of_correct_answers == 1) && (%ml_points_bonus > 0)) {
n390=      inc %points %ml_points_bonus
n391=      %multiplier_str = BONUS
n392=    }
n393=    if (($hget(multipliers, %player) > 1) && (%ml_no_multiplier == 0)) {
n394=      %multiplier = $hget(multipliers, %player)
n395=      if (%multiplier_str != $null) {
n396=        %multiplier_str = %multiplier_str $+ , $+ $chr(32)
n397=      }
n398=      if (%multiplier == 2) {
n399=        %multiplier_str = %multiplier_str COMBO
n400=      }
n401=      else {
n402=        if (%ml_no_trimbo == 0) {
n403=          %multiplier_str = %multiplier_str TRIMBO
n404=        }
n405=        else {
n406=          %multiplier_str = %multiplier_str COMBO
n407=          %multiplier = 2
n408=        }
n409=      }
n410=    }
n411=    if (%multiplier_str != $null) {
n412=      %multiplier_str = $chr(32) $+ - $+ $chr(32) $+ %multiplier_str
n413=    }
n414=
n415=    %points = %points * %multiplier
n416=    hadd question_points %player %points
n417=
n418=    var %point_description = %points $+ %multiplier_str
n419=    if (%points > 0) {
n420=      %point_description = + $+ %point_description
n421=    }
n422=    var %description = $hget(question_points, %i).item ( $+ %point_description $+ )
n423=    hadd point_descriptions %player %description
n424=
n425=    var %total_player_points 0
n426=    if ($hget(total_points, $hget(question_points, %i).item)) {
n427=      %total_player_points = $hget(total_points, $hget(question_points, %i).item) + %points
n428=    }
n429=    else {
n430=      inc %total_player_points %points
n431=    }
n432=    if (%ml_points_for_answers == 1) {
n433=      inc %total_player_points
n434=    }
n435=    hadd -m total_points $hget(question_points, %i).item %total_player_points
n436=
n437=    inc %i
n438=  }
n439=
n440=  var %winners_str
n441=  var %losers_str
n442=  hmake players_found
n443=  %i = 1
n444=  while ($hget(question_points, %i).item) {
n445=    var %max = -999999
n446=    var %min = 999999
n447=    var %max_player
n448=    var %min_player
n449=
n450=    var %j = 1
n451=    while ($hget(question_points, %j).item) {
n452=      if ($hget(question_points, %j).data > %max && !$hget(players_found, $hget(question_points, %j).item)) {
n453=        %max = $hget(question_points, %j).data
n454=        %max_player = $hget(question_points, %j).item
n455=      }
n456=      if ($hget(question_points, %j).data < %min && !$hget(players_found, $hget(question_points, %j).item)) {
n457=        %min = $hget(question_points, %j).data
n458=        %min_player = $hget(question_points, %j).item
n459=      }
n460=      inc %j
n461=    }
n462=
n463=    if (%max < 0 && %min > 0) {
n464=      break
n465=    }
n466=
n467=    if (%max > 0) {
n468=      if (%winners_str != $null) {
n469=        %winners_str = %winners_str $+ , $+ $chr(32)
n470=      }
n471=      %winners_str = %winners_str $hget(point_descriptions, %max_player)
n472=      hadd players_found %max_player 1
n473=    }
n474=    if (%min < 0) {
n475=      if (%losers_str != $null) {
n476=        %losers_str = %losers_str $+ , $+ $chr(32)
n477=      }
n478=      %losers_str = %losers_str $hget(point_descriptions, %min_player)
n479=      hadd players_found %min_player 1
n480=    }
n481=
n482=    inc %i
n483=  }
n484=  hfree players_found
n485=  hfree point_descriptions
n486=
n487=  ; aktualizacja mnoznikow
n488=  %i = 1
n489=  while ($hget(multipliers, %i).item) {
n490=    var %player = $hget(multipliers, %i).item
n491=    if (!$hget(question_points, %player) || $hget(question_points, %player) <= 0) {
n492=      hadd multipliers %player 1
n493=    }
n494=    inc %i
n495=  }
n496=
n497=  %i = 1
n498=  while ($hget(question_points, %i).item) {
n499=    var %player = $hget(question_points, %i).item
n500=    if ($hget(question_points, %player) > 0) {
n501=      var %multiplier 1
n502=      if (!$hget(multipliers, %player)) {
n503=        %multiplier = 2
n504=      }
n505=      else {
n506=        %multiplier = $hget(multipliers, %player)
n507=        if (%multiplier < 3) {
n508=          inc %multiplier
n509=        }
n510=      }
n511=      hadd -m multipliers %player %multiplier
n512=    }
n513=    inc %i
n514=  }
n515=
n516=  msg %ml_quiz_channel 9,1Prawidlowa odpowiedz podali:11,2 %winners_str
n517=  msg %ml_quiz_channel 4,1Punkty traca:4,2 %losers_str
n518=  unset %ml_round_*
n519=  if ($hget(question_points)) {
n520=    hfree question_points
n521=  }
n522=
n523=  var %nextdelay = %ml_question_delay
n524=  if ((5 // %ml_question_number) && (%ml_question_number < %ml_number_of_questions)) {
n525=    var %statdelay = %ml_question_delay / 2
n526=    .timerStats 1 %statdelay mstat 4
n527=    inc %nextdelay 5
n528=  }
n529=
n530=  if (%ml_running == 1) {
n531=    if (%ml_question_number == %ml_number_of_questions) {
n532=      .timerNext 1 3 end_of_questions
n533=    }
n534=    else {
n535=      .timerNext 1 %nextdelay next_question
n536=    }
n537=  }
n538=}
n539=
n540=end_of_questions {
n541=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Koniec pytan 2,2...................................................
n542=  halt
n543=}
n544=
n545=/mstat {
n546=  var %number_of_places = 99
n547=  if ($1 != $null) {
n548=    %number_of_places = $1
n549=  }
n550=
n551=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n552=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Punktacja po 0,1 %ml_question_number $+ . 08,02 rundach (0,1 %quiz_duration 8,2)2,2 $&
n553=    $adjust_length(3, %ml_question_number) $+ $adjust_length(31, %quiz_duration)
n554=
n555=  var %i = 1
n556=  var %prev_place = 1
n557=  var %prev_points = 10000
n558=  hmake found_stats
n559=  while ($hget(total_points, %i).item && %i <= %number_of_places) {
n560=    var %max = -100000
n561=    var %j = 1
n562=    var %max_player
n563=
n564=    while ($hget(total_points, %j).item) {
n565=      if ($hget(total_points, %j).data > %max && !$hget(found_stats, $hget(total_points, %j).item)) {
n566=        %max = $hget(total_points, %j).data
n567=        %max_player = $hget(total_points, %j).item
n568=      }
n569=      inc %j
n570=    }
n571=
n572=    hadd found_stats %max_player 1
n573=
n574=    var %place
n575=    if (%max == %prev_points) {
n576=      %place = %prev_place
n577=    }
n578=    else {
n579=      %place = %i
n580=    }
n581=
n582=    var %player_correct_answers = 0
n583=    var %player_incorrect_answers = 0
n584=    var %player_answers = 0
n585=    if ($hget(correct_answers, %max_player)) {
n586=      %player_correct_answers = $hget(correct_answers, %max_player)
n587=    }
n588=    if ($hget(incorrect_answers, %max_player)) {
n589=      %player_incorrect_answers = $hget(incorrect_answers, %max_player)
n590=    }
n591=    %player_answers = %player_correct_answers + %player_incorrect_answers
n592=    var %line = 2,2 $+ $adjust_length(3, %place) $+ 8,2 $+ $chr(32) $+ %place $+ . $&
n593=      Miejsce: 0,2 $+ %max_player $+ 2,2 $+ $adjust_length(26, %max_player) $+ 0,2 (2 $+ $adjust_length(3, %max)
n594=    if (%max > 0) {
n595=      %line = %line $+ 9 %max
n596=    }
n597=    elseif (%max < 0) {
n598=      %line = %line $+ 4 %max
n599=    }
n600=    else {
n601=      %line = %line $+ 11 %max
n602=    }
n603=    %line = %line $+ 0 $get_point_word(%max) $+ 2 $+ $adjust_length(7, $get_point_word(%max)) $+ 0), $&
n604=      odp2 $adjust_length(3, %player_answers) $+ 11 $+ $eval(%player_answers) $+ 0:2 $+ $adjust_length(3, %player_correct_answers) $+ 9 $&
n605=      $chr(32) $+ %player_correct_answers $+ 0 $+ $chr(32) $+ / $+ 2 $+ $adjust_length(3, %player_incorrect_answers) $+ 4 $+ $chr(32) $+ %player_incorrect_answers
n606=    msg %ml_quiz_channel %line $+ 2,2 ...... 
n607=
n608=    %prev_points = %max
n609=    %prev_place = %place
n610=    inc %i
n611=  }
n612=
n613=  hfree found_stats
n614=}
n615=
n616=/mkom {
n617=  window -c @Milioner
n618=  window -l @Milioner 0 0 580 440 courier new
n619=  aline @Milioner      8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz - Spis komend ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n620=  aline @Milioner 4/mon <#kanal> 6- wlaczenie quizu
n621=  aline @Milioner 4/mtryb [normal|punkt] 6- ustawienie trybu, punkt - dodawanie punktu za kazda udzielona odpowiedz (domyslnie: normal)
n622=  aline @Milioner 4/mtryb [pub|priv] 6- ustawienie trybu, pub - odpowiedzi na kanale i prywatnie, priv - tylko odpowiedzi prywatne (domyslnie)
n623=  aline @Milioner 4/mtryb [1234|abcd] 6- ustawienie trybu, odpowiedzi typu A/B/C/D (domyslnie) lub 1/2/3/4, ale mozna podac dowolne 4 rozne od siebie znaki
n624=  aline @Milioner 4/mtryb [mnoznik] 6- wlaczenie/wylaczenie naliczania mnoznika za serie odpowiedzi
n625=  aline @Milioner 4/mtryb [trimbo] 6- wlaczenie/wylaczenie najwyzszego mnoznika x3 (TRIMBO)
n626=  aline @Milioner 4/mtryb [antygoogler] 6- wlaczenie/wylaczenie antygooglera chroniacego pytania
n627=  aline @Milioner 4/mplik <nazwapliku> 6- ustawienie pliku z pytaniami oraz uruchomienie quizu
n628=  aline @Milioner 4/mzasady 6- wyswietlenie informacji o zasadach gry
n629=  aline @Milioner 4/mpodp <podpowiedz> 6- podanie podpowiedzi
n630=  aline @Milioner 4/mstat [<liczba_miejsc>] 6- wyswietlenie wynikow quizu dla podanej liczby miejsc (jest nie podana - wyswietla pelna liste)
n631=  aline @Milioner 4/mwersja 6- informacja o wersji skryptu
n632=  aline @Milioner 4/mczas <czas_w_sekundach> 6- ustawienie czasu na odpowiedz
n633=  aline @Milioner 4/minterwal <czas_w_sekundach> 6- ustawienie opoznienia miedzy odpowiedzia a nastepnym pytaniem
n634=  aline @Milioner 4/mkanal <#kanal> 6- przeniesienie quizu na inny kanal
n635=  aline @Milioner 4/mpunkt <nick> [<ilosc_punktow>] 6- dodaje podanej osobie punkty (1 - jesli nie podano liczby)
n636=  aline @Milioner 4/mpauza 6- wstrzymanie/wznowienie quizu
n637=  aline @Milioner 4/mile <nick> 6- wyswietla ilosc punktow podanego gracza
n638=  aline @Milioner 4/mblokuj <nick> [<powod>] 6- uniemozliwia gre podanemu graczowi
n639=  aline @Milioner 4/modblokuj <nick> [<powod>] 6- przywraca gracza do gry
n640=  aline @Milioner 4/moff 6- wylaczenie quizu
n641=  aline @Milioner           8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n642=  aline @Milioner 6Plik z pytaniami do Quizu to zwykly plik txt. Musi on wygladac nastepujaco:
n643=  aline @Milioner Pytanie pierwsze
n644=  aline @Milioner Odpowiedz prawidlowa
n645=  aline @Milioner Odpowiedz bledna 1
n646=  aline @Milioner Odpowiedz bledna 2
n647=  aline @Milioner Odpowiedz bledna 3
n648=  aline @Milioner Pytanie drugie
n649=  aline @Milioner ...itp...
n650=  aline @Milioner 6Skrypt losowo wybiera literki przyporzadkowane poszczegolnym pytaniom.
n651=  aline @Milioner 6UWAGA: Przed rozpoczeciem Quizu Milionerowego nalezy zakonczyc wszystkie
n652=  aline @Milioner 6inne formy quizow (Redhead, Autoquiz itp.), a najlepiej wyladowac je z
n653=  aline @Milioner 6pamieci komenda /unload. Uniemozliwi to wykorzystanie przez pomylke
n654=  aline @Milioner 6komend z tych quizow, co spowodowaloby bledy w aktualnie prowadzonym
n655=  aline @Milioner 6Quizie Milionerowym.
n656=  aline @Milioner 6Jezeli macie inne pytania lub sugestie: 12http://www.quizpl.net
n657=}
n658=
n659=/mzasady {
n660=  msg %ml_quiz_channel 2,2 .8,2Punktowanie: pierwszy odpowiadajacy za dobra odpowiedz 11,2+3 pkt8,2, $&
n661=    za zla 4,2-3 pkt8,2, drugi odpowiadajacy odpowiednio 11,2+/4,2-2 pkt8,2, pozostali 11,2+/4,2-1 pkt8,2, $&
n662=    jesli poprawnie odpowiedziala tylko 1 osoba: BONUS 11,2+1 pkt8,2. 
n663=  if (%ml_no_multiplier == 0) {
n664=    if (%ml_no_trimbo == 0) {
n665=      msg %ml_quiz_channel 2,2 .8,2Poprawna odpowiedz oznacza, iz w nastepnym pytaniu punkty mnozone sa 11,2razy 2 (COMBO)8,2, $&
n666=        dwie i wiecej poprawnych odpowiedzi z rzedu powoduje mnozenie punktow w nastepnych pytaniach 11,2razy 3 (TRIMBO)8,2. $&
n667=        Uwaga! Mnozone sa punkty tak dodatnie jak i ujemne 4,2(!)8,2, a wiec mozna za pytanie zdobyc11,2 12 pkt8,2 $&
n668=        lub stracic4,2 9 pkt8,2; niepoprawna odpowiedz lub brak odpowiedzi kasuje mnoznik. 
n669=    }
n670=    else {
n671=      msg %ml_quiz_channel 2,2 .8,2Poprawna odpowiedz oznacza, iz w kazdym nastepnym pytaniu punkty mnozone sa 11,2razy 2 (COMBO)8,2. $&
n672=        Uwaga! Mnozone sa punkty tak dodatnie jak i ujemne 4,2(!)8,2, a wiec mozna za pytanie zdobyc11,2 8 pkt8,2 $&
n673=        lub stracic4,2 6 pkt8,2; niepoprawna odpowiedz lub brak odpowiedzi kasuje mnoznik. 
n674=    }
n675=  }
n676=  if (%ml_points_for_answers == 1) {
n677=    msg %ml_quiz_channel 2,2 .8,2Tryb punkt za odpowiedz oznacza, iz za kazda udzielona odpowiedz, bez wzgledu na to czy poprawna $&
n678=      czy bledna, gracz uzyskuje dodatkowo 11,2+1 pkt8,2. 
n679=  }
n680=}
n681=
n682=/mkanal {
n683=  set %ml_quiz_channel $1
n684=  echo 9,1 MILIONEROWY 08Quiz:8,2 Quiz przeniesiony na kanal 0,1 %ml_quiz_channel 2,2 $adjust_length(34, %ml_quiz_channel)
n685=}
n686=
n687=; player, points
n688=/mpunkt {
n689=  var %points 1
n690=  if ($2 != $null) {
n691=    %points = $2
n692=  }
n693=
n694=  var %total_player_points %points
n695=  if ($hget(total_points, $1)) {
n696=    inc %total_player_points $hget(total_points, $1)
n697=  }
n698=  hadd -m total_points $1 %total_player_points
n699=
n700=  echo 9,1 MILIONEROWY 08Quiz:8,2 Dodalem 0,1 $1 8,2 %points $get_point_word(%points) 02,02 $&
n701=    $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(40, $1)
n702=}
n703=
n704=; czyje, publicznie (1/0)
n705=/mile {
n706=  if ($hget(total_points, $1)) {
n707=    var %points = $hget(total_points, $1)
n708=    var %better_players = 0
n709=    var %i = 1
n710=    while ($hget(total_points, %i).item) {
n711=      if ($hget(total_points, %i).data > %points) {
n712=        inc %better_players
n713=      }
n714=      inc %i
n715=    }
n716=
n717=    var %place = %better_players + 1
n718=    if ($2 == 1) {
n719=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jest na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n720=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(25, $1)
n721=    }
n722=    else {
n723=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jestes na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n724=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(23, $1)
n725=    }
n726=  }
n727=  else {
n728=    if ($2 == 1) {
n729=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie ma jeszcze zadnego punktu2,2 $adjust_length(31, $1)
n730=    }
n731=    else {
n732=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie masz jeszcze zadnego punktu2,2 $adjust_length(29, $1)
n733=    }
n734=  }
n735=}
n736=
n737=/mpauza {
n738=  if (%ml_number_of_questions != $null) {
n739=    if (%ml_running == 1) {
n740=      set %ml_running 0
n741=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wstrzymana 2,2.................................................
n742=    }
n743=    else {
n744=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wznowiona 2,2..................................................
n745=      set %ml_running 1
n746=      next_question
n747=    }
n748=  }
n749=}
n750=
n751=//mwersja {
n752=  msg %ml_quiz_channel 1,1................9,1 MILIONEROWY 8Quiz (wersja %ml_version $+ ) by 0KrzS&Dizzy&wilk 1,1.................
n753=  msg %ml_quiz_channel 2,2.......................8,2 Sciagaj z: 0http://www.quizpl.net2,2 ..........................
n754=}
n755=
n756=/mczas {
n757=  set %ml_answer_time $1
n758=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas na odpowiedz: 0,1 %ml_answer_time 8,2 sekund(y)2,2 $adjust_length(3, %ml_answer_time) $+ .............................
n759=  zapiszconf
n760=}
n761=
n762=/minterwal {
n763=  set %ml_question_delay $1 
n764=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas pomiedzy pytaniami: 0,1 %ml_question_delay 8,2 sekund(y)2,2 $adjust_length(3, %ml_question_delay) $+ .......................
n765=  zapiszconf
n766=}
n767=
n768=/mdelay minterwal $1-
n769=
n770=/mpodp {
n771=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Podpowiedz: 0,1 $1- 2,2 $adjust_length(49, $1-)
n772=}
n773=
n774=//mblokuj {
n775=  hadd -m locked_players $1 1
n776=  var %reason $2-
n777=  if (%reason == $null) {
n778=    %reason = nie podano
n779=  }
n780=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Odpadasz z gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n781=}
n782=
n783=//modblokuj {
n784=  hdel locked_players $1
n785=  var %reason $2-
n786=  if (%reason == $null) {
n787=    %reason = nie podano
n788=  }
n789=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Wracasz do gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n790=}
n791=
n792=/wczytajconf {
n793=  var %tmp = $readini(milioner_conf.ini, milioner, PunktZaOdpowiedz)
n794=  if (%tmp isnum) {
n795=    if (%tmp > 0) set %ml_points_for_answers 1
n796=    else set %ml_points_for_answers 0
n797=  }
n798=  %tmp = $readini(milioner_conf.ini, milioner, OdpowiedziPubliczne)
n799=  if (%tmp isnum) {
n800=    if (%tmp > 0) set %ml_public 1
n801=    else set %ml_public 0
n802=  }
n803=  %tmp = $readini(milioner_conf.ini, milioner, BezMnoznika)
n804=  if (%tmp isnum) {
n805=    if (%tmp > 0) set %ml_no_multiplier 1
n806=    else set %ml_no_multiplier 0
n807=  }
n808=  %tmp = $readini(milioner_conf.ini, milioner, BezTrimbo)
n809=  if (%tmp isnum) {
n810=    if (%tmp > 0) set %ml_no_trimbo 1
n811=    else set %ml_no_trimbo 0
n812=  }
n813=  %tmp = $readini(milioner_conf.ini, milioner, AntyGoogler)
n814=  if (%tmp isnum) {
n815=    if (%tmp > 0) set %ml_antigoogler 1
n816=    else set %ml_antigoogler 0
n817=  }
n818=  %tmp = $readini(milioner_conf.ini, milioner, CzasNaOdpowiedz)
n819=  if ((%tmp isnum) && (%tmp > 0)) set %ml_answer_time %tmp
n820=  %tmp = $readini(milioner_conf.ini, milioner, OpoznieniePytan)
n821=  if ((%tmp isnum) && (%tmp > 0)) set %ml_question_delay %tmp
n822=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaPierwszego)
n823=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_first %tmp
n824=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaDrugiego)
n825=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_second %tmp
n826=  %tmp = $readini(milioner_conf.ini, milioner, PunktyDlaReszty)
n827=  if ((%tmp isnum) && (%tmp > 0)) set %ml_points_rest %tmp
n828=  %tmp = $readini(milioner_conf.ini, milioner, PunktyZaBonus)
n829=  if ((%tmp isnum) && (%tmp >= 0)) set %ml_points_bonus %tmp
n830=  %tmp = $readini(milioner_conf.ini, milioner, Symbole)
n831=  if ((%tmp != $null) && ($len(%tmp) == 4)) {
n832=    var %tmp_a = $upper($left(%tmp, 1))
n833=    var %tmp_b = $upper($mid(%tmp, 2, 1))
n834=    var %tmp_c = $upper($mid(%tmp, 3, 1))
n835=    var %tmp_d = $upper($right(%tmp, 1))
n836=    if ((%tmp_a != %tmp_b) && (%tmp_a != %tmp_c) && (%tmp_a != %tmp_d) && (%tmp_b != %tmp_c) && (%tmp_b != %tmp_d) && (%tmp_c != %tmp_d)) {
n837=      set %ml_a %tmp_a
n838=      set %ml_b %tmp_b
n839=      set %ml_c %tmp_c
n840=      set %ml_d %tmp_d
n841=    }
n842=  }
n843=}
n844=
n845=/zapiszconf {
n846=  writeini -n milioner_conf.ini milioner PunktZaOdpowiedz %ml_points_for_answers
n847=  writeini -n milioner_conf.ini milioner CzasNaOdpowiedz %ml_answer_time
n848=  writeini -n milioner_conf.ini milioner OpoznieniePytan %ml_question_delay
n849=  writeini -n milioner_conf.ini milioner OdpowiedziPubliczne %ml_public
n850=  writeini -n milioner_conf.ini milioner Symbole %ml_a $+ %ml_b $+ %ml_c $+ %ml_d
n851=  writeini -n milioner_conf.ini milioner BezMnoznika %ml_no_multiplier
n852=  writeini -n milioner_conf.ini milioner BezTrimbo %ml_no_trimbo
n853=  writeini -n milioner_conf.ini milioner AntyGoogler %ml_antigoogler
n854=  writeini -n milioner_conf.ini milioner PunktyDlaPierwszego %ml_points_first
n855=  writeini -n milioner_conf.ini milioner PunktyDlaDrugiego %ml_points_second
n856=  writeini -n milioner_conf.ini milioner PunktyDlaReszty %ml_points_rest
n857=  writeini -n milioner_conf.ini milioner PunktyZaBonus %ml_points_bonus
n858=}
