[aliases]
n0=; Milionerowy QuizÂ® by KrzS&Dizzy
n1=; Radze tu nic nie zmieniac
n2=; mod by wilk (odpowiadanie na # i/lub priv, dodatkowy komunikat w /mtryb, tryb 1234)
n3=
n4=//mon {
n5=  if ($1 == $null) {
n6=    echo -a 9,1 MILIONEROWY 8QUIZ Podaj nazwe kanalu na ktorym chcesz grac, np:  "/mon #kanal"
n7=    halt
n8=  }
n9=  unset %ml_*
n10=
n11=  set %ml_version 3.04
n12=  set %ml_answer_time 10
n13=  set %ml_question_delay 20
n14=  set %ml_question_number 0
n15=  set %ml_points_for_answers 0
n16=  set %ml_public 0
n17=  set %ml_a A
n18=  set %ml_b B
n19=  set %ml_c C
n20=  set %ml_d D
n21=
n22=  load -rs milioner2.ini
n23=  set %ml_quiz_channel $$1
n24=  if ($hget(total_points)) {
n25=    hfree total_points
n26=  }
n27=  if ($hget(multipliers)) {
n28=    hfree multipliers
n29=  }
n30=  if ($hget(correct_answers)) {
n31=    hfree correct_answers
n32=  }
n33=  if ($hget(incorrect_answers)) {
n34=    hfree incorrect_answers
n35=  }
n36=  if ($hget(locked_players)) {
n37=    hfree locked_players
n38=  }
n39=
n40=  echo 4Milionerowy Quiz (wersja %ml_version $+ ) wystartowal ! 
n41=  echo 4Spis komend /mkom
n42=  msg %ml_quiz_channel 1,1 ............9MILIONEROWY8 Quiz 8,1(wersja %ml_version $+ ) wystartowal na 0 $+ %ml_quiz_channel 1........... $adjust_length(11, %ml_quiz_channel)
n43=  msg %ml_quiz_channel 2,2 ....8,2 !zasady - zasady,2,2..8,2!ile - miejsce,2,2..8,2punktacja automatycznie co 5 pytan  2......
n44=  if (%ml_public == 0) {
n45=    msg %ml_quiz_channel 2,2 ...........15,2 >>> odpowiedzi udzielamy PRYWATNIE puszczajacemu quiz <<< 2............
n46=  }
n47=  else {
n48=    msg %ml_quiz_channel 2,2 .....15,2 >>> odpowiedzi mozna udzielac takze prywatnie puszczajacemu quiz <<< 2.......
n49=  }
n50=
n51=  set %ml_start_time $ctime
n52=}
n53=
n54=//mtryb {
n55=  if ($1 == punkt) {
n56=    set %ml_points_for_answers 1
n57=    msg %ml_quiz_channel 2,2 ............................11,2 Tryb: punkt za odpowiedz 2............................
n58=  }
n59=  elseif ($1 == normal) {
n60=    set %ml_points_for_answers 0
n61=    msg %ml_quiz_channel 2,2 ............................11,2 Tryb: punktowanie zwykle 2............................
n62=  }
n63=  elseif ($1 == pub) {
n64=    set %ml_public 1
n65=    msg %ml_quiz_channel 2,2 .................11,2 Tryb: mozna odpowiadac na kanale lub prywatnie 2.................
n66=  }
n67=  elseif ($1 == priv) {
n68=    set %ml_public 0
n69=    msg %ml_quiz_channel 2,2 ........................11,2 Tryb: odpowiedzi TYLKO prywatnie 2........................
n70=  }
n71=  elseif ($len($1) == 4) {
n72=    var %ml_tmp_1 = $upper($left($1, 1))
n73=    var %ml_tmp_2 = $upper($mid($1, 2, 1))
n74=    var %ml_tmp_3 = $upper($mid($1, 3, 1))
n75=    var %ml_tmp_4 = $upper($right($1, 1))
n76=    if ((%ml_tmp_1 != %ml_tmp_2) && (%ml_tmp_1 != %ml_tmp_3) && (%ml_tmp_1 != %ml_tmp_4) && (%ml_tmp_2 != %ml_tmp_3) && (%ml_tmp_2 != %ml_tmp_4) && (%ml_tmp_3 != %ml_tmp_4)) {
n77=      set %ml_a %ml_tmp_1
n78=      set %ml_b %ml_tmp_2
n79=      set %ml_c %ml_tmp_3
n80=      set %ml_d %ml_tmp_4
n81=      msg %ml_quiz_channel 2,2 ............................11,2 Tryb: odpowiedzi %ml_a $+ / $+ %ml_b $+ / $+ %ml_c $+ / $+ %ml_d 2............................
n82=    }
n83=  }
n84=}
n85=
n86=//moff {
n87=  .timerAnswers off
n88=  .timerWarning off
n89=  .timerResult off
n90=  .timerStats off
n91=  .timerNext off
n92=
n93=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n94=  msg %ml_quiz_channel 1,1 ..........9MILIONEROWY8 Quiz 8,1(wersja %ml_version by KrzS&Dizzy&wilk) zakonczony 1...........
n95=  if (%ml_question_number == 1) { 
n96=    msg %ml_quiz_channel 2,2 ......................8,2 Czas gry: 0,1 %ml_question_number 8,2 runda w 0,1 %quiz_duration 2,2 $& 
n97=      $adjust_length(3, %ml_question_number) $+ $adjust_length(32, %quiz_duration)
n98=  }
n99=  else { 
n100=    msg %ml_quiz_channel 2,2 ......................8,2 Czas gry: 0,1 %ml_question_number 8,2 rund w 0,1 %quiz_duration 2,2 $&
n101=      $adjust_length(3, %ml_question_number) $+ $adjust_length(33, %quiz_duration)
n102=  }
n103=  msg %ml_quiz_channel 2,2 .......................8,2 Sciagaj z: 0,2http://www.quizpl.net 2,2.........................
n104=  .unload -rs milioner2.ini 
n105=  unset %ml_*
n106=  .rlevel =2
n107=}
n108=
n109=/mplik { 
n110=  set %ml_file $1-
n111=  if ($exists(%ml_file) == $false) {
n112=    echo 9,1 MILIONEROWY 08Quiz:8,2 Plik 0,1 %ml_file 8,2 nie istnieje 2,2 $adjust_length(42, %ml_file)
n113=    halt
n114=  }
n115=
n116=  set %ml_number_of_questions $lines(%ml_file) / 5
n117=  set %ml_line_number 0 
n118=  echo 9,1 MILIONEROWY 08Quiz:8,2 Plik z pytaniami: 0,1 $nopath(%ml_file) 08,02, liczba pytan: 0,1 %ml_number_of_questions 2,2 $&
n119=    $adjust_length(22, $nopath(%ml_file)) $+ $adjust_length(3, %ml_number_of_questions)
n120=
n121=  if (%ml_question_delay == $null) {
n122=    set %ml_question_delay 10
n123=  }
n124=
n125=  set %ml_running 1
n126=  .timer 1 3 next_question
n127=}
n128=
n129=get_number_of_dots { 
n130=  var %dot_length $len($2-)
n131=  dec %dot_length 7
n132=  %dot_length = $1 - %dot_length
n133=  return %dot_length 
n134=}
n135=
n136=add_dots { 
n137=  var %dots = .
n138=  while ($len(%dots) < $get_number_of_dots($1,$2-)) { 
n139=    %dots = %dots $+ . 
n140=  }
n141=  return %dots
n142=}
n143=
n144=; ile znakow, tekst
n145=adjust_length {
n146=  if ($len($2-) >= $1) {
n147=    return
n148=  } 
n149=
n150=  var %dots = .
n151=  var %total_length = $len($2-) + $len(%dots)
n152=  while (%total_length < $1) {
n153=    %dots = %dots $+ .
n154=    inc %total_length
n155=  }
n156=
n157=  return %dots
n158=}
n159=
n160=antigoogle {
n161=  var %replacement =  $+ $2 $+ a08
n162=  return $replace($1,$chr(32),%replacement)
n163=}
n164=
n165=get_point_word { 
n166=  if ($1 == $null) {
n167=    return
n168=  }
n169=  var %number = $abs($1)
n170=
n171=  if (%number == 1) {
n172=    return punkt
n173=  } 
n174=  elseif ((%number > 10) && (%number < 20)) {
n175=    return punktow
n176=  } 
n177=  elseif (($right(%number,1) > 1) && ($right(%number,1) < 5)) {
n178=    return punkty
n179=  } 
n180=  else {
n181=    return punktow
n182=  }
n183=}
n184=
n185=read_line {
n186=  var %line $1
n187=  var %file_name $2-
n188=  var %command -l $+ %line 
n189=  return $read %command %file_name
n190=}
n191=
n192=next_question {
n193=  if (%ml_running == 0) {
n194=    return
n195=  }
n196=
n197=  inc %ml_line_number
n198=  var %current_question = $read_line(%ml_line_number, %ml_file)
n199=  if (%ml_line_number < $lines(%ml_file)) {
n200=
n201=    hmake answers
n202=    hmake random_answers
n203=    var %i
n204=
n205=    ; reading answers
n206=    %i = 1
n207=    while (%i <= 4) {
n208=      inc %ml_line_number
n209=      hadd answers $chr($calc(64 + %i)) $read_line(%ml_line_number, %ml_file)
n210=      inc %i
n211=    }
n212=
n213=    ; randomizing answers
n214=    %i = 1
n215=    while (%i <= 4) {
n216=      var %answer_number = $rand(1, $calc(5 - %i))  
n217=      var %answer_key = $hget(answers, %answer_number).item
n218=      var %answer_description = $+($chr(3), 08, %ml_ [ $+ [ $chr($calc(96 + %i)) ] ], :, $chr(32), $chr(3), 08, $hget(answers, %answer_number).data)
n219=
n220=      hadd random_answers $chr($calc(64 + %i)) %answer_description      
n221=      if (%answer_number == 1 && %ml_round_correct_answer == $null) {
n222=        set %ml_round_correct_answer %ml_ [ $+ [ $chr($calc(96 + %i)) ] ]
n223=        set %ml_round_correct_answer_description $hget(answers, %answer_number).data
n224=      }
n225=      hdel answers %answer_key
n226=
n227=      inc %i
n228=    }
n229=
n230=    inc %ml_question_number
n231=    set %ml_round_question %current_question
n232=    set %ml_round_answer_a $hget(random_answers, A)
n233=    set %ml_round_answer_b $hget(random_answers, B)
n234=    set %ml_round_answer_c $hget(random_answers, C)
n235=    set %ml_round_answer_d $hget(random_answers, D)
n236=    $display_question
n237=
n238=    hfree random_answers
n239=    hfree answers
n240=  } 
n241=  else {
n242=    end_of_questions
n243=  }
n244=}
n245=
n246=display_question { 
n247=  msg %ml_quiz_channel 8,1Pytanie $+ $chr(32) $+ %ml_question_number $+ / $+ %ml_number_of_questions $+ :8,1 $antigoogle(%ml_round_question, 01) 1,1 $add_dots(75, %ml_round_question)
n248=  .timerAnswers 1 3 display_answers
n249=}
n250=
n251=display_answers {
n252=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_a, 02) $+ 2,2 $add_dots(45, %ml_round_answer_a) 1,1.08,02 $antigoogle(%ml_round_answer_b, 02) $+ 2,2 $add_dots(45, %ml_round_answer_b)
n253=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_c, 02) $+ 2,2 $add_dots(45, %ml_round_answer_c) 1,1.08,02 $antigoogle(%ml_round_answer_d, 02) $+ 2,2 $add_dots(45, %ml_round_answer_d)
n254=  echo 4 Prawidlowa odpowiedz to: %ml_round_correct_answer - %ml_round_correct_answer_description
n255=  .timerWarning 1 %ml_answer_time display_warning
n256=  set %ml_round_running 1
n257=}
n258=
n259=display_warning {
n260=  msg %ml_quiz_channel 8,1Uwaga:8,2 zostalo 10 sekund
n261=  .timerResult 1 10 display_result
n262=}
n263=
n264=; nick, 1 poprawna
n265=//register_answer {
n266=  var %points
n267=  if (!%ml_round_answered_first) {
n268=    %points = 3
n269=    set %ml_round_answered_first 1
n270=  } 
n271=  elseif (!%ml_round_answered_second) {
n272=    %points = 2
n273=    set %ml_round_answered_second 1
n274=  } 
n275=  else {
n276=    %points = 1
n277=  }
n278=  if ($2 != 1) {
n279=    %points = -1 * %points
n280=    if (!$hget(incorrect_answers, $1)) {
n281=      hadd -m incorrect_answers $1 1
n282=    } 
n283=    else {
n284=      var %ile = $hget(incorrect_answers, $1) + 1
n285=      hadd -m incorrect_answers $1 %ile
n286=    }
n287=  } 
n288=  else {
n289=    if (!$hget(correct_answers, $1)) {
n290=      hadd -m correct_answers $1 1
n291=    } 
n292=    else {
n293=      var %ile = $hget(correct_answers, $1) + 1
n294=      hadd -m correct_answers $1 %ile
n295=    }
n296=  }
n297=
n298=  hadd -m question_points $1 %points
n299=}
n300=
n301=; nick, answer
n302=//question_answered {
n303=  if (%ml_round_running == 1 && $1 ison %ml_quiz_channel && !$hget(question_points, $1) && !$hget(locked_players, $1)) {
n304=    if (%ml_round_correct_answer == $2) {
n305=      register_answer $nick 1
n306=    }
n307=    else {
n308=      register_answer $nick 0
n309=    }
n310=  }
n311=}
n312=
n313=//display_result {
n314=  set %ml_round_running 0
n315=  msg %ml_quiz_channel 8,1Koniec Czasu:8,2 Prawidlowa odpowiedz to:9,2 %ml_round_correct_answer - %ml_round_correct_answer_description
n316=  var %i
n317=  var %number_of_correct_answers = 0
n318=
n319=  %i = 1
n320=  while ($hget(question_points, %i).item) {
n321=    if ($hget(question_points, %i).data > 0) {
n322=      inc %number_of_correct_answers
n323=    }
n324=    inc %i
n325=  }  
n326=
n327=  ; aktualizacja punktow
n328=  hmake point_descriptions
n329=  %i = 1
n330=  while ($hget(question_points, %i).item) {
n331=    var %player = $hget(question_points, %i).item
n332=    var %points = $hget(question_points, %i).data
n333=    var %multiplier 1
n334=    var %multiplier_str
n335=
n336=    if (%points > 0 && %number_of_correct_answers == 1) {
n337=      inc %points
n338=      %multiplier_str = BONUS
n339=    }
n340=    if ($hget(multipliers, %player) > 1) {
n341=      %multiplier = $hget(multipliers, %player)
n342=      if (%multiplier_str != $null) {
n343=        %multiplier_str = %multiplier_str $+ , $+ $chr(32)
n344=      }
n345=      if (%multiplier == 2) {
n346=        %multiplier_str = %multiplier_str COMBO
n347=      }
n348=      else {
n349=        %multiplier_str = %multiplier_str TRIMBO
n350=      }
n351=    }
n352=    if (%multiplier_str != $null) {
n353=      %multiplier_str = $chr(32) $+ - $+ $chr(32) $+ %multiplier_str
n354=    }
n355=
n356=    %points = %points * %multiplier
n357=    hadd question_points %player %points
n358=
n359=    var %point_description = %points $+ %multiplier_str
n360=    if (%points > 0) {
n361=      %point_description = + $+ %point_description
n362=    }
n363=    var %description = $hget(question_points, %i).item ( $+ %point_description $+ )
n364=    hadd point_descriptions %player %description
n365=
n366=    var %total_player_points 0
n367=    if ($hget(total_points, $hget(question_points, %i).item)) {
n368=      %total_player_points = $hget(total_points, $hget(question_points, %i).item) + %points
n369=    } 
n370=    else {
n371=      inc %total_player_points %points
n372=    }
n373=    if (%ml_points_for_answers == 1) {
n374=      inc %total_player_points
n375=    }
n376=    hadd -m total_points $hget(question_points, %i).item %total_player_points
n377=
n378=    inc %i
n379=  }
n380=
n381=  var %winners_str
n382=  var %losers_str
n383=  hmake players_found
n384=  %i = 1
n385=  while ($hget(question_points, %i).item) {
n386=    var %max = -999999
n387=    var %min = 999999
n388=    var %max_player
n389=    var %min_player
n390=
n391=    var %j = 1
n392=    while ($hget(question_points, %j).item) {
n393=      if ($hget(question_points, %j).data > %max && !$hget(players_found, $hget(question_points, %j).item)) {
n394=        %max = $hget(question_points, %j).data
n395=        %max_player = $hget(question_points, %j).item
n396=      }
n397=      if ($hget(question_points, %j).data < %min && !$hget(players_found, $hget(question_points, %j).item)) {
n398=        %min = $hget(question_points, %j).data
n399=        %min_player = $hget(question_points, %j).item
n400=      }      
n401=      inc %j
n402=    }
n403=
n404=    if (%max < 0 && %min > 0) {
n405=      break
n406=    }
n407=
n408=    if (%max > 0) {
n409=      if (%winners_str != $null) {
n410=        %winners_str = %winners_str $+ , $+ $chr(32)
n411=      }
n412=      %winners_str = %winners_str $hget(point_descriptions, %max_player)
n413=      hadd players_found %max_player 1
n414=    }
n415=    if (%min < 0) {
n416=      if (%losers_str != $null) {
n417=        %losers_str = %losers_str $+ , $+ $chr(32)
n418=      }
n419=      %losers_str = %losers_str $hget(point_descriptions, %min_player)
n420=      hadd players_found %min_player 1
n421=    }
n422=
n423=    inc %i
n424=  }
n425=  hfree players_found
n426=  hfree point_descriptions
n427=
n428=  ; aktualizacja mnoznikow
n429=  %i = 1
n430=  while ($hget(multipliers, %i).item) {
n431=    var %player = $hget(multipliers, %i).item
n432=    if (!$hget(question_points, %player) || $hget(question_points, %player) <= 0) {
n433=      hadd multipliers %player 1
n434=    }
n435=    inc %i
n436=  }
n437=
n438=  %i = 1
n439=  while ($hget(question_points, %i).item) {
n440=    var %player = $hget(question_points, %i).item
n441=    if ($hget(question_points, %player) > 0) {
n442=      var %multiplier 1
n443=      if (!$hget(multipliers, %player)) {
n444=        %multiplier = 2
n445=      } 
n446=      else {
n447=        %multiplier = $hget(multipliers, %player)
n448=        if (%multiplier < 3) {
n449=          inc %multiplier
n450=        }
n451=      }
n452=      hadd -m multipliers %player %multiplier
n453=    }
n454=    inc %i
n455=  }
n456=
n457=  msg %ml_quiz_channel 9,1Prawidlowa odpowiedz podali:11,2 %winners_str
n458=  msg %ml_quiz_channel 4,1Punkty traca:4,2 %losers_str
n459=  unset %ml_round_*
n460=  if ($hget(question_points)) {
n461=    hfree question_points
n462=  }
n463=
n464=  var %nextdelay = %ml_question_delay
n465=  if ((5 // %ml_question_number) && (%ml_question_number < %ml_number_of_questions)) {
n466=    var %statdelay = %ml_question_delay / 2
n467=    .timerStats 1 %statdelay mstat 4
n468=    inc %nextdelay 5
n469=  }
n470=
n471=  if (%ml_running == 1) {
n472=    if (%ml_question_number == %ml_number_of_questions) {
n473=      .timerNext 1 3 end_of_questions
n474=    } 
n475=    else {
n476=      .timerNext 1 %nextdelay next_question 
n477=    }
n478=  }
n479=}
n480=
n481=end_of_questions {
n482=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Koniec pytan 2,2...................................................
n483=  halt
n484=}
n485=
n486=/mstat {
n487=  var %number_of_places = 99
n488=  if ($1 != $null) {
n489=    %number_of_places = $1
n490=  }
n491=
n492=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n493=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Punktacja po 0,1 %ml_question_number $+ . 08,02 rundach (0,1 %quiz_duration 8,2)2,2 $&
n494=    $adjust_length(3, %ml_question_number) $+ $adjust_length(31, %quiz_duration)
n495=
n496=  var %i = 1
n497=  var %prev_place = 1
n498=  var %prev_points = 10000
n499=  hmake found_stats
n500=  while ($hget(total_points, %i).item && %i <= %number_of_places) {
n501=    var %max = -100000
n502=    var %j = 1
n503=    var %max_player
n504=
n505=    while ($hget(total_points, %j).item) {
n506=      if ($hget(total_points, %j).data > %max && !$hget(found_stats, $hget(total_points, %j).item)) {
n507=        %max = $hget(total_points, %j).data
n508=        %max_player = $hget(total_points, %j).item
n509=      }
n510=      inc %j
n511=    }
n512=
n513=    hadd found_stats %max_player 1
n514=
n515=    var %place
n516=    if (%max == %prev_points) {
n517=      %place = %prev_place
n518=    }
n519=    else {
n520=      %place = %i
n521=    }
n522=
n523=    var %player_correct_answers = 0
n524=    var %player_incorrect_answers = 0
n525=    var %player_answers = 0
n526=    if ($hget(correct_answers, %max_player)) {
n527=      %player_correct_answers = $hget(correct_answers, %max_player)
n528=    }
n529=    if ($hget(incorrect_answers, %max_player)) {
n530=      %player_incorrect_answers = $hget(incorrect_answers, %max_player)
n531=    }
n532=    %player_answers = %player_correct_answers + %player_incorrect_answers
n533=    var %line = 2,2 $+ $adjust_length(3, %place) $+ 8,2 $+ $chr(32) $+ %place $+ . $&
n534=      Miejsce: 0,2 $+ %max_player $+ 2,2 $+ $adjust_length(26, %max_player) $+ 0,2 (2 $+ $adjust_length(3, %max)
n535=    if (%max > 0) {
n536=      %line = %line $+ 9 %max
n537=    }
n538=    elseif (%max < 0) {
n539=      %line = %line $+ 4 %max
n540=    }
n541=    else {
n542=      %line = %line $+ 11 %max
n543=    }
n544=    %line = %line $+ 0 $get_point_word(%max) $+ 2 $+ $adjust_length(7, $get_point_word(%max)) $+ 0), $&
n545=      odp2 $adjust_length(3, %player_answers) $+ 11 $+ $eval(%player_answers) $+ 0:2 $+ $adjust_length(3, %player_correct_answers) $+ 9 $&
n546=      $chr(32) $+ %player_correct_answers $+ 0 $+ $chr(32) $+ / $+ 2 $+ $adjust_length(3, %player_incorrect_answers) $+ 4 $+ $chr(32) $+ %player_incorrect_answers
n547=    msg %ml_quiz_channel %line $+ 2,2 ...... 
n548=
n549=    %prev_points = %max
n550=    %prev_place = %place
n551=    inc %i
n552=  }
n553=
n554=  hfree found_stats
n555=}
n556=
n557=/mkom {
n558=  window -c @Milioner
n559=  window -l @Milioner 0 0 580 440 courier new
n560=  aline @Milioner      8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz - Spis komend ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n561=  aline @Milioner 4/mon <#kanal> 6- wlaczenie quizu
n562=  aline @Milioner 4/mtryb [normal|punkt] 6- ustawienie trybu, punkt - dodawanie punktu za kazda udzielona odpowiedz (domyslnie: normal)
n563=  aline @Milioner 4/mtryb [pub|priv] 6- ustawienie trybu, pub - odpowiedzi na kanale i prywatnie, priv - tylko odpowiedzi prywatne (domyslnie)
n564=  aline @Milioner 4/mtryb [1234|abcd] 6- ustawienie trybu, odpowiedzi typu A/B/C/D (domyslnie) lub 1/2/3/4, ale mozna podac dowolne 4 rozne znaki
n565=  aline @Milioner 4/mplik <nazwapliku> 6- ustawienie pliku z pytaniami oraz uruchomienie quizu
n566=  aline @Milioner 4/mzasady 6- wyswietlenie informacji o zasadach gry
n567=  aline @Milioner 4/mpodp <podpowiedz> 6- podanie podpowiedzi
n568=  aline @Milioner 4/mstat [<liczba_miejsc>] 6- wyswietlenie wynikow quizu dla podanej liczby miejsc (jest nie podana - wyswietla pelna liste)
n569=  aline @Milioner 4/mwersja 6- informacja o wersji skryptu
n570=  aline @Milioner 4/mczas <czas_w_sekundach> 6- ustawienie czasu na odpowiedz
n571=  aline @Milioner 4/minterwal <czas_w_sekundach> 6- ustawienie opoznienia miedzy odpowiedzia a nastepnym pytaniem
n572=  aline @Milioner 4/mkanal <#kanal> 6- przeniesienie quizu na inny kanal
n573=  aline @Milioner 4/mpunkt <nick> [<ilosc_punktow>] 6- dodaje podanej osobie punkty (1 - jesli nie podano liczby)
n574=  aline @Milioner 4/mpauza 6- wstrzymanie/wznowienie quizu
n575=  aline @Milioner 4/mile <nick> 6- wyswietla ilosc punktow podanego gracza
n576=  aline @Milioner 4/mblokuj <nick> [<powod>] 6- uniemozliwia gre podanemu graczowi
n577=  aline @Milioner 4/modblokuj <nick> [<powod>] 6- przywraca gracza do gry
n578=  aline @Milioner 4/moff 6- wylaczenie quizu
n579=  aline @Milioner           8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n580=  aline @Milioner 6Plik z pytaniami do Quizu to zwykly plik txt. Musi on wygladac nastepujaco:
n581=  aline @Milioner Pytanie pierwsze
n582=  aline @Milioner Odpowiedz prawidlowa 
n583=  aline @Milioner Odpowiedz bledna 1
n584=  aline @Milioner Odpowiedz bledna 2
n585=  aline @Milioner Odpowiedz bledna 3
n586=  aline @Milioner Pytanie drugie
n587=  aline @Milioner ...itp...
n588=  aline @Milioner 6Skrypt losowo wybiera literki przyporzadkowane poszczegolnym pytaniom.
n589=  aline @Milioner 6UWAGA: Przed rozpoczeciem Quizu Milionerowego nalezy zakonczyc wszystkie
n590=  aline @Milioner 6inne formy quizow (Redhead, Autoquiz itp.), a najlepiej wyladowac je z 
n591=  aline @Milioner 6pamieci komenda /unload. Uniemozliwi to wykorzystanie przez pomylke 
n592=  aline @Milioner 6komend z tych quizow, co spowodowaloby bledy w aktualnie prowadzonym
n593=  aline @Milioner 6Quizie Milionerowym.
n594=  aline @Milioner 6Jezeli macie inne pytania lub sugestie: 12http://www.quizpl.net
n595=}
n596=
n597=/mzasady {
n598=  msg %ml_quiz_channel 2,2 .8,2Punktowanie: pierwszy odpowiadajacy za dobra odpowiedz 11,2+3 pkt8,2, $&
n599=    za zla 4,2-3 pkt8,2, drugi odpowiadajacy odpowiednio 11,2+/4,2-2 pkt8,2, pozostali 11,2+/4,2-1 pkt8,2, $&
n600=    jesli poprawnie odpowiedziala tylko 1 osoba: BONUS 11,2+1 pkt8,2.
n601=  msg %ml_quiz_channel 2,2 .8,2Poprawna odpowiedz oznacza, iz w nastepnym pytaniu punkty mnozone sa 11,2razy 2 (COMBO)8,2, $&
n602=    dwie i wiecej poprawnych odpowiedzi z rzedu powoduje mnozenie punktow w nastepnych pytaniach 11,2razy 3 (TRIMBO)8,2. $&
n603=    Uwaga! Mnozone sa punkty tak dodatnie jak i ujemne 4,2(!)8,2, a wiec mozna za pytanie zdobyc11,2 12 pkt8,2 $&
n604=    lub stracic4,2 9 pkt8,2; niepoprawna odpowiedz lub brak odpowiedzi kasuje mnoznik.
n605=  if (%ml_points_for_answers == 1) {
n606=    msg %ml_quiz_channel 2,2 .8,2Tryb punkt za odpowiedz oznacza, iz za kazda udzielona odpowiedz, bez wzgledu na to czy poprawna $&
n607=      czy bledna, gracz uzyskuje dodatkowo 11,2+1 pkt8,2.
n608=  }
n609=}
n610=
n611=/mkanal {
n612=  set %ml_quiz_channel $1
n613=  echo 9,1 MILIONEROWY 08Quiz:8,2 Quiz przeniesiony na kanal 0,1 %ml_quiz_channel 2,2 $adjust_length(34, %ml_quiz_channel)
n614=}
n615=
n616=; player, points
n617=/mpunkt {
n618=  var %points 1
n619=  if ($2 != $null) {
n620=    %points = $2
n621=  }
n622=
n623=  var %total_player_points %points
n624=  if ($hget(total_points, $1)) {
n625=    inc %total_player_points $hget(total_points, $1)
n626=  }
n627=  hadd -m total_points $1 %total_player_points
n628=
n629=  echo 9,1 MILIONEROWY 08Quiz:8,2 Dodalem 0,1 $1 8,2 %points $get_point_word(%points) 02,02 $&
n630=    $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(40, $1)
n631=}
n632=
n633=; czyje, publicznie (1/0)
n634=/mile {
n635=  if ($hget(total_points, $1)) {
n636=    var %points = $hget(total_points, $1)
n637=    var %better_players = 0
n638=    var %i = 1
n639=    while ($hget(total_points, %i).item) {
n640=      if ($hget(total_points, %i).data > %points) {
n641=        inc %better_players
n642=      }
n643=      inc %i
n644=    }
n645=
n646=    var %place = %better_players + 1
n647=    if ($2 == 1) {
n648=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jest na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n649=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(25, $1)
n650=    } 
n651=    else {
n652=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jestes na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n653=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(23, $1)
n654=    }
n655=  } 
n656=  else {
n657=    if ($2 == 1) {
n658=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie ma jeszcze zadnego punktu2,2 $adjust_length(31, $1)
n659=    } 
n660=    else {
n661=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie masz jeszcze zadnego punktu2,2 $adjust_length(29, $1)
n662=    }
n663=  }
n664=}
n665=
n666=/mpauza {
n667=  if (%ml_number_of_questions != $null) {
n668=    if (%ml_running == 1) {
n669=      set %ml_running 0
n670=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wstrzymana 2,2.................................................
n671=    }
n672=    else { 
n673=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wznowiona 2,2..................................................
n674=      set %ml_running 1
n675=      next_question 
n676=    }
n677=  }
n678=}
n679=
n680=//mwersja {
n681=  msg %ml_quiz_channel 1,1................9,1 MILIONEROWY 8Quiz (wersja %ml_version $+ ) by 0KrzS&Dizzy&wilk 1,1................
n682=  msg %ml_quiz_channel 2,2.......................8,2 Sciagaj z: 0http://www.quizpl.net2,2 ..........................
n683=}
n684=
n685=/mczas { 
n686=  set %ml_answer_time $1
n687=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas na odpowiedz: 0,1 %ml_answer_time 8,2 sekund(y)2,2 $adjust_length(3, %ml_answer_time) $+ .............................
n688=  set %ml_answer_time %ml_answer_time - 10
n689=}
n690=
n691=/minterwal { 
n692=  set %ml_question_delay $1 
n693=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas pomiedzy pytaniami: 0,1 %ml_question_delay 8,2 sekund(y)2,2 $adjust_length(3, %ml_question_delay) $+ .......................
n694=}
n695=
n696=/mdelay minterwal $1-
n697=
n698=/mpodp { 
n699=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Podpowiedz: 0,1 $1- 2,2 $adjust_length(49, $1-)
n700=}
n701=
n702=//mblokuj {
n703=  hadd -m locked_players $1 1
n704=  var %reason $2-
n705=  if (%reason == $null) {
n706=    %reason = nie podano
n707=  }
n708=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Odpadasz z gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n709=}
n710=
n711=//modblokuj {
n712=  hdel locked_players $1
n713=  var %reason $2-
n714=  if (%reason == $null) {
n715=    %reason = nie podano
n716=  }
n717=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Wracasz do gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n718=}
