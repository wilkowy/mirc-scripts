[aliases]
n0=; Milionerowy QuizÂ® by KrzS&Dizzy
n1=; Radze tu nic nie zmieniac
n2=; mod by wilk (odpowiadanie takze na priv)
n3=
n4=//mon {
n5=  if ($1 == $null) {
n6=    echo -a 9,1 MILIONEROWY 8QUIZ Podaj nazwe kanalu na ktorym chcesz grac, np:  "/mon #kanal"
n7=    halt
n8=  }
n9=  unset %ml_*
n10=
n11=  set %ml_version 3.01
n12=  set %ml_answer_time 10
n13=  set %ml_question_delay 20
n14=  set %ml_question_number 0
n15=
n16=  load -rs milioner2.ini
n17=  set %ml_quiz_channel $$1
n18=  if ($hget(total_points)) {
n19=    hfree total_points
n20=  }
n21=  if ($hget(multipliers)) {
n22=    hfree multipliers
n23=  }
n24=  if ($hget(correct_answers)) {
n25=    hfree correct_answers
n26=  }
n27=  if ($hget(incorrect_answers)) {
n28=    hfree incorrect_answers
n29=  }
n30=  if ($hget(locked_players)) {
n31=    hfree locked_players
n32=  }
n33=
n34=  echo 4Milionerowy Quiz (wersja %ml_version $+ ) wystartowal ! 
n35=  echo 4Spis komend /mkom
n36=  msg %ml_quiz_channel 1,1 ............9MILIONEROWY8 Quiz 8,1(wersja %ml_version $+ ) wystartowal na 0 $+ %ml_quiz_channel 1........... $adjust_length(11, %ml_quiz_channel)
n37=  msg %ml_quiz_channel 2,2 ....8,2 !zasady - zasady,2,2..8,2!ile - miejsce,2,2..8,2punktacja automatycznie co 5 pytan  2......
n38=  msg %ml_quiz_channel 2,2 ........7,2 (odpowiedzi mozna udzielac takze prywatnie puszczajacemu quiz) 2..........
n39=
n40=  set %ml_start_time $ctime
n41=}
n42=
n43=//mtryb {
n44=  if ($1 == punkt) {
n45=    set %ml_points_for_answers 1
n46=    msg %ml_quiz_channel 2,2 ............................4,2 Tryb: punkt za odpowiedz  2............................
n47=  }
n48=  else {
n49=    set %ml_points_for_answers 0
n50=    msg %ml_quiz_channel 2,2 ............................4,2 Tryb: punktowanie zwykle 2.............................
n51=  }
n52=}
n53=
n54=//moff {
n55=  .timerAnswers off
n56=  .timerWarning off
n57=  .timerResult off
n58=  .timerStats off
n59=  .timerNext off
n60=
n61=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n62=  msg %ml_quiz_channel 1,1 ..........9MILIONEROWY8 Quiz 8,1(wersja %ml_version by KrzS&Dizzy&wilk) zakonczony 1...........
n63=  if (%ml_question_number == 1) { 
n64=    msg %ml_quiz_channel 2,2 .......................8,2 Czas gry: 0,1 %ml_question_number 8,2 runda: 0,1 %quiz_duration 2,2 $& 
n65=      $adjust_length(3, %ml_question_number) $+ $adjust_length(32, %quiz_duration)
n66=  }
n67=  else { 
n68=    msg %ml_quiz_channel 2,2 .......................8,2 Czas gry: 0,1 %ml_question_number 8,2 rund: 0,1 %quiz_duration 2,2 $&
n69=      $adjust_length(3, %ml_question_number) $+ $adjust_length(33, %quiz_duration)
n70=  }
n71=  msg %ml_quiz_channel 2,2 .......................8,2 Sciagaj z: 0,2http://www.quizpl.net 2,2.........................
n72=  .unload -rs milioner2.ini 
n73=  unset %ml_*
n74=  .rlevel =2
n75=}
n76=
n77=/mplik { 
n78=  set %ml_file $1-
n79=  if ($exists(%ml_file) == $false) {
n80=    echo 9,1 MILIONEROWY 08Quiz:8,2 Plik 0,1 %ml_file 8,2 nie istnieje 2,2 $adjust_length(42, %ml_file)
n81=    halt
n82=  }
n83=
n84=  set %ml_number_of_questions $lines(%ml_file) / 5
n85=  set %ml_line_number 0 
n86=  echo 9,1 MILIONEROWY 08Quiz:8,2 Plik z pytaniami: 0,1 $nopath(%ml_file) 08,02, liczba pytan: 0,1 %ml_number_of_questions 2,2 $&
n87=    $adjust_length(22, $nopath(%ml_file)) $+ $adjust_length(3, %ml_number_of_questions)
n88=
n89=  if (%ml_question_delay == $null) {
n90=    set %ml_question_delay 10
n91=  }
n92=
n93=  set %ml_running 1
n94=  .timer 1 3 next_question
n95=}
n96=
n97=get_number_of_dots { 
n98=  var %dot_length $len($2-)
n99=  dec %dot_length 7
n100=  %dot_length = $1 - %dot_length
n101=  return %dot_length 
n102=}
n103=
n104=add_dots { 
n105=  var %dots = .
n106=  while ($len(%dots) < $get_number_of_dots($1,$2-)) { 
n107=    %dots = %dots $+ . 
n108=  }
n109=  return %dots
n110=}
n111=
n112=; ile znakow, tekst
n113=adjust_length {
n114=  if ($len($2-) >= $1) {
n115=    return
n116=  } 
n117=
n118=  var %dots = .
n119=  var %total_length = $len($2-) + $len(%dots)
n120=  while (%total_length < $1) {
n121=    %dots = %dots $+ .
n122=    inc %total_length
n123=  }
n124=
n125=  return %dots
n126=}
n127=
n128=antigoogle {
n129=  var %replacement =  $+ $2 $+ a08
n130=  return $replace($1,$chr(32),%replacement)
n131=}
n132=
n133=get_point_word { 
n134=  if ($1 == $null) {
n135=    return
n136=  }
n137=  var %number = $abs($1)
n138=
n139=  if (%number == 1) {
n140=    return punkt
n141=  } 
n142=  elseif ((%number > 10) && (%number < 20)) {
n143=    return punktow
n144=  } 
n145=  elseif (($right(%number,1) > 1) && ($right(%number,1) < 5)) {
n146=    return punkty
n147=  } 
n148=  else {
n149=    return punktow
n150=  }
n151=}
n152=
n153=read_line {
n154=  var %line $1
n155=  var %file_name $2-
n156=  var %command -l $+ %line 
n157=  return $read %command %file_name
n158=}
n159=
n160=next_question {
n161=  if (%ml_running == 0) {
n162=    return
n163=  }
n164=
n165=  inc %ml_line_number
n166=  var %current_question = $read_line(%ml_line_number, %ml_file)
n167=  if (%ml_line_number < $lines(%ml_file)) {
n168=
n169=    hmake answers
n170=    hmake random_answers
n171=    var %i
n172=
n173=    ; reading answers
n174=    %i = 1
n175=    while (%i <= 4) {
n176=      inc %ml_line_number
n177=      hadd answers $chr($calc(64 + %i)) $read_line(%ml_line_number, %ml_file)
n178=      inc %i
n179=    }
n180=
n181=    ; randomizing answers
n182=    %i = 1
n183=    while (%i <= 4) {
n184=      var %answer_number = $rand(1, $calc(5 - %i))  
n185=      var %answer_key = $hget(answers, %answer_number).item
n186=      var %answer_description = $+($chr(3), 08, $chr($calc(64 + %i)), :, $chr(32), $chr(3), 08, $hget(answers, %answer_number).data)
n187=
n188=      hadd random_answers $chr($calc(64 + %i)) %answer_description      
n189=      if (%answer_number == 1 && %ml_round_correct_answer == $null) {
n190=        set %ml_round_correct_answer $chr($calc(64 + %i))
n191=        set %ml_round_correct_answer_description $hget(answers, %answer_number).data
n192=      }
n193=      hdel answers %answer_key
n194=
n195=      inc %i
n196=    }
n197=
n198=    inc %ml_question_number
n199=    set %ml_round_question %current_question
n200=    set %ml_round_answer_a $hget(random_answers, A)
n201=    set %ml_round_answer_b $hget(random_answers, B)
n202=    set %ml_round_answer_c $hget(random_answers, C)
n203=    set %ml_round_answer_d $hget(random_answers, D)
n204=    $display_question
n205=
n206=    hfree random_answers
n207=    hfree answers
n208=  } 
n209=  else {
n210=    end_of_questions
n211=  }
n212=}
n213=
n214=display_question { 
n215=  msg %ml_quiz_channel 8,1Pytanie $+ $chr(32) $+ %ml_question_number $+ / $+ %ml_number_of_questions $+ :8,1 $antigoogle(%ml_round_question, 01) 1,1 $add_dots(75, %ml_round_question)
n216=  .timerAnswers 1 3 display_answers
n217=}
n218=
n219=display_answers {
n220=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_a, 02) $+ 2,2 $add_dots(45, %ml_round_answer_a) 1,1.08,02 $antigoogle(%ml_round_answer_b, 02) $+ 2,2 $add_dots(45, %ml_round_answer_b)
n221=  msg %ml_quiz_channel 08,02 $antigoogle(%ml_round_answer_c, 02) $+ 2,2 $add_dots(45, %ml_round_answer_c) 1,1.08,02 $antigoogle(%ml_round_answer_d, 02) $+ 2,2 $add_dots(45, %ml_round_answer_d)
n222=  echo 4 Prawidlowa odpowiedz to: %ml_round_correct_answer - %ml_round_correct_answer_description
n223=  .timerWarning 1 %ml_answer_time display_warning
n224=  set %ml_round_running 1
n225=}
n226=
n227=display_warning {
n228=  msg %ml_quiz_channel 8,1Uwaga:8,2 zostalo 10 sekund
n229=  .timerResult 1 10 display_result
n230=}
n231=
n232=; nick, 1 poprawna
n233=//register_answer {
n234=  var %points
n235=  if (!%ml_round_answered_first) {
n236=    %points = 3
n237=    set %ml_round_answered_first 1
n238=  } 
n239=  elseif (!%ml_round_answered_second) {
n240=    %points = 2
n241=    set %ml_round_answered_second 1
n242=  } 
n243=  else {
n244=    %points = 1
n245=  }
n246=  if ($2 != 1) {
n247=    %points = -1 * %points
n248=    if (!$hget(incorrect_answers, $1)) {
n249=      hadd -m incorrect_answers $1 1
n250=    } 
n251=    else {
n252=      var %ile = $hget(incorrect_answers, $1) + 1
n253=      hadd -m incorrect_answers $1 %ile
n254=    }
n255=  } 
n256=  else {
n257=    if (!$hget(correct_answers, $1)) {
n258=      hadd -m correct_answers $1 1
n259=    } 
n260=    else {
n261=      var %ile = $hget(correct_answers, $1) + 1
n262=      hadd -m correct_answers $1 %ile
n263=    }
n264=  }
n265=
n266=  hadd -m question_points $1 %points
n267=}
n268=
n269=; nick, answer
n270=//question_answered {
n271=  if (%ml_round_running == 1 && $1 ison %ml_quiz_channel && !$hget(question_points, $1) && !$hget(locked_players, $1)) {
n272=    if (%ml_round_correct_answer == $2) {
n273=      register_answer $nick 1
n274=    }
n275=    else {
n276=      register_answer $nick 0
n277=    }
n278=  }
n279=}
n280=
n281=//display_result {
n282=  set %ml_round_running 0
n283=  msg %ml_quiz_channel 8,1Koniec Czasu:8,2 Prawidlowa odpowiedz to:9,2 %ml_round_correct_answer - %ml_round_correct_answer_description
n284=  var %i
n285=  var %number_of_correct_answers = 0
n286=
n287=  %i = 1
n288=  while ($hget(question_points, %i).item) {
n289=    if ($hget(question_points, %i).data > 0) {
n290=      inc %number_of_correct_answers
n291=    }
n292=    inc %i
n293=  }  
n294=
n295=  ; aktualizacja punktow
n296=  hmake point_descriptions
n297=  %i = 1
n298=  while ($hget(question_points, %i).item) {
n299=    var %player = $hget(question_points, %i).item
n300=    var %points = $hget(question_points, %i).data
n301=    var %multiplier 1
n302=    var %multiplier_str
n303=
n304=    if (%points > 0 && %number_of_correct_answers == 1) {
n305=      inc %points
n306=      %multiplier_str = BONUS
n307=    }
n308=    if ($hget(multipliers, %player) > 1) {
n309=      %multiplier = $hget(multipliers, %player)
n310=      if (%multiplier_str != $null) {
n311=        %multiplier_str = %multiplier_str $+ , $+ $chr(32)
n312=      }
n313=      if (%multiplier == 2) {
n314=        %multiplier_str = %multiplier_str COMBO
n315=      }
n316=      else {
n317=        %multiplier_str = %multiplier_str TRIMBO
n318=      }
n319=    }
n320=    if (%multiplier_str != $null) {
n321=      %multiplier_str = $chr(32) $+ - $+ $chr(32) $+ %multiplier_str
n322=    }
n323=
n324=    %points = %points * %multiplier
n325=    hadd question_points %player %points
n326=
n327=    var %point_description = %points $+ %multiplier_str
n328=    if (%points > 0) {
n329=      %point_description = + $+ %point_description
n330=    }
n331=    var %description = $hget(question_points, %i).item ( $+ %point_description $+ )
n332=    hadd point_descriptions %player %description
n333=
n334=    var %total_player_points 0
n335=    if ($hget(total_points, $hget(question_points, %i).item)) {
n336=      %total_player_points = $hget(total_points, $hget(question_points, %i).item) + %points
n337=    } 
n338=    else {
n339=      inc %total_player_points %points
n340=    }
n341=    if (%ml_points_for_answers == 1) {
n342=      inc %total_player_points
n343=    }
n344=    hadd -m total_points $hget(question_points, %i).item %total_player_points
n345=
n346=    inc %i
n347=  }
n348=
n349=  var %winners_str
n350=  var %losers_str
n351=  hmake players_found
n352=  %i = 1
n353=  while ($hget(question_points, %i).item) {
n354=    var %max = -999999
n355=    var %min = 999999
n356=    var %max_player
n357=    var %min_player
n358=
n359=    var %j = 1
n360=    while ($hget(question_points, %j).item) {
n361=      if ($hget(question_points, %j).data > %max && !$hget(players_found, $hget(question_points, %j).item)) {
n362=        %max = $hget(question_points, %j).data
n363=        %max_player = $hget(question_points, %j).item
n364=      }
n365=      if ($hget(question_points, %j).data < %min && !$hget(players_found, $hget(question_points, %j).item)) {
n366=        %min = $hget(question_points, %j).data
n367=        %min_player = $hget(question_points, %j).item
n368=      }      
n369=      inc %j
n370=    }
n371=
n372=    if (%max < 0 && %min > 0) {
n373=      break
n374=    }
n375=
n376=    if (%max > 0) {
n377=      if (%winners_str != $null) {
n378=        %winners_str = %winners_str $+ , $+ $chr(32)
n379=      }
n380=      %winners_str = %winners_str $hget(point_descriptions, %max_player)
n381=      hadd players_found %max_player 1
n382=    }
n383=    if (%min < 0) {
n384=      if (%losers_str != $null) {
n385=        %losers_str = %losers_str $+ , $+ $chr(32)
n386=      }
n387=      %losers_str = %losers_str $hget(point_descriptions, %min_player)
n388=      hadd players_found %min_player 1
n389=    }
n390=
n391=    inc %i
n392=  }
n393=  hfree players_found
n394=  hfree point_descriptions
n395=
n396=  ; aktualizacja mnoznikow
n397=  %i = 1
n398=  while ($hget(multipliers, %i).item) {
n399=    var %player = $hget(multipliers, %i).item
n400=    if (!$hget(question_points, %player) || $hget(question_points, %player) <= 0) {
n401=      hadd multipliers %player 1
n402=    }
n403=    inc %i
n404=  }
n405=
n406=  %i = 1
n407=  while ($hget(question_points, %i).item) {
n408=    var %player = $hget(question_points, %i).item
n409=    if ($hget(question_points, %player) > 0) {
n410=      var %multiplier 1
n411=      if (!$hget(multipliers, %player)) {
n412=        %multiplier = 2
n413=      } 
n414=      else {
n415=        %multiplier = $hget(multipliers, %player)
n416=        if (%multiplier < 3) {
n417=          inc %multiplier
n418=        }
n419=      }
n420=      hadd -m multipliers %player %multiplier
n421=    }
n422=    inc %i
n423=  }
n424=
n425=  msg %ml_quiz_channel 9,1Prawidlowa odpowiedz podali:11,2 %winners_str
n426=  msg %ml_quiz_channel 4,1Punkty traca:4,2 %losers_str
n427=  unset %ml_round_*
n428=  if ($hget(question_points)) {
n429=    hfree question_points
n430=  }
n431=
n432=  var %nextdelay = %ml_question_delay
n433=  if ((5 // %ml_question_number) && (%ml_question_number < %ml_number_of_questions)) {
n434=    var %statdelay = %ml_question_delay / 2
n435=    .timerStats 1 %statdelay mstat 4
n436=    inc %nextdelay 5
n437=  }
n438=
n439=  if (%ml_running == 1) {
n440=    if (%ml_question_number == %ml_number_of_questions) {
n441=      .timerNext 1 3 end_of_questions
n442=    } 
n443=    else {
n444=      .timerNext 1 %nextdelay next_question 
n445=    }
n446=  }
n447=}
n448=
n449=end_of_questions {
n450=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Koniec pytan 2,2...................................................
n451=  halt
n452=}
n453=
n454=/mstat {
n455=  var %number_of_places = 99
n456=  if ($1 != $null) {
n457=    %number_of_places = $1
n458=  }
n459=
n460=  var %quiz_duration = $duration($calc($ctime - %ml_start_time))
n461=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Punktacja po 0,1 %ml_question_number $+ . 08,02 rundach (0,1 %quiz_duration 8,2)2,2 $&
n462=    $adjust_length(3, %ml_question_number) $+ $adjust_length(31, %quiz_duration)
n463=
n464=  var %i = 1
n465=  var %prev_place = 1
n466=  var %prev_points = 10000
n467=  hmake found_stats
n468=  while ($hget(total_points, %i).item && %i <= %number_of_places) {
n469=    var %max = -100000
n470=    var %j = 1
n471=    var %max_player
n472=
n473=    while ($hget(total_points, %j).item) {
n474=      if ($hget(total_points, %j).data > %max && !$hget(found_stats, $hget(total_points, %j).item)) {
n475=        %max = $hget(total_points, %j).data
n476=        %max_player = $hget(total_points, %j).item
n477=      }
n478=      inc %j
n479=    }
n480=
n481=    hadd found_stats %max_player 1
n482=
n483=    var %place
n484=    if (%max == %prev_points) {
n485=      %place = %prev_place
n486=    }
n487=    else {
n488=      %place = %i
n489=    }
n490=
n491=    var %player_correct_answers = 0
n492=    var %player_incorrect_answers = 0
n493=    var %player_answers = 0
n494=    if ($hget(correct_answers, %max_player)) {
n495=      %player_correct_answers = $hget(correct_answers, %max_player)
n496=    }
n497=    if ($hget(incorrect_answers, %max_player)) {
n498=      %player_incorrect_answers = $hget(incorrect_answers, %max_player)
n499=    }
n500=    %player_answers = %player_correct_answers + %player_incorrect_answers
n501=    var %line = 2,2 $+ $adjust_length(3, %place) $+ 8,2 $+ $chr(32) $+ %place $+ . $&
n502=      Miejsce: 0,2 $+ %max_player $+ 2,2 $+ $adjust_length(26, %max_player) $+ 0,2 (2 $+ $adjust_length(3, %max)
n503=    if (%max > 0) {
n504=      %line = %line $+ 9 %max
n505=    }
n506=    elseif (%max < 0) {
n507=      %line = %line $+ 4 %max
n508=    }
n509=    else {
n510=      %line = %line $+ 11 %max
n511=    }
n512=    %line = %line $+ 0 $get_point_word(%max) $+ 2 $+ $adjust_length(7, $get_point_word(%max)) $+ 0), $&
n513=      odp2 $adjust_length(3, %player_answers) $+ 11 $+ $eval(%player_answers) $+ 0:2 $+ $adjust_length(3, %player_correct_answers) $+ 9 $&
n514=      $chr(32) $+ %player_correct_answers $+ 0 $+ $chr(32) $+ / $+ 2 $+ $adjust_length(3, %player_incorrect_answers) $+ 4 $+ $chr(32) $+ %player_incorrect_answers
n515=    msg %ml_quiz_channel %line $+ 2,2 ...... 
n516=
n517=    %prev_points = %max
n518=    %prev_place = %place
n519=    inc %i
n520=  }
n521=
n522=  hfree found_stats
n523=}
n524=
n525=/mkom {
n526=  window -c @Milioner
n527=  window -l @Milioner 0 0 580 440 courier new
n528=  aline @Milioner      8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz - Spis komend ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n529=  aline @Milioner 4/mon <#kanal> 6- wlaczenie quizu
n530=  aline @Milioner 4/mtryb [normalny|punkt] 6- ustawienie trybu, punkt - dodawanie punktu za kazda udzielona odpowiedz
n531=  aline @Milioner 4/mplik <nazwapliku> 6- ustawienie pliku z pytaniami oraz uruchomienie quizu
n532=  aline @Milioner 4/mzasady 6- wyswietlenie informacji o zasadach gry
n533=  aline @Milioner 4/mpodp <podpowiedz> 6- podanie podpowiedzi
n534=  aline @Milioner 4/mstat [<liczba_miejsc>] 6- wyswietlenie wynikow quizu dla podanej liczby miejsc (jest nie podana - wyswietla pelna liste)
n535=  aline @Milioner 4/mwersja 6- informacja o wersji skryptu
n536=  aline @Milioner 4/mczas <czas_w_sekundach> 6- ustawienie czasu na odpowiedz
n537=  aline @Milioner 4/minterwal <czas_w_sekundach> 6- ustawienie opoznienia miedzy odpowiedzia a nastepnym pytaniem
n538=  aline @Milioner 4/mkanal <#kanal> 6- przeniesienie quizu na inny kanal
n539=  aline @Milioner 4/mpunkt <nick> [<ilosc_punktow>] 6- dodaje podanej osobie punkty (1 - jesli nie podano liczby)
n540=  aline @Milioner 4/mpauza 6- wstrzymanie/wznowienie quizu
n541=  aline @Milioner 4/mile <nick> 6- wyswietla ilosc punktow podanego gracza
n542=  aline @Milioner 4/mblokuj <nick> [<powod>] 6- uniemozliwia gre podanemu graczowi
n543=  aline @Milioner 4/modblokuj <nick> [<powod>] 6- przywraca gracza do gry
n544=  aline @Milioner 4/moff 6- wylaczenie quizu
n545=  aline @Milioner           8,0%0,8%9,8'%8,9%,3,9'%9,3%,1,3'%3,1%, 8,1 ### Milionerowy Quiz ### 3,1'%1,3%,9,3'%3,9%,8,9'%9,8%,0,8'%8,0%,
n546=  aline @Milioner 6Plik z pytaniami do Quizu to zwykly plik txt. Musi on wygladac nastepujaco:
n547=  aline @Milioner Pytanie pierwsze
n548=  aline @Milioner Odpowiedz prawidlowa 
n549=  aline @Milioner Odpowiedz bledna 1
n550=  aline @Milioner Odpowiedz bledna 2
n551=  aline @Milioner Odpowiedz bledna 3
n552=  aline @Milioner Pytanie drugie
n553=  aline @Milioner ...itp...
n554=  aline @Milioner 6Skrypt losowo wybiera literki przyporzadkowane poszczegolnym pytaniom.
n555=  aline @Milioner 6UWAGA: Przed rozpoczeciem Quizu Milionerowego nalezy zakonczyc wszystkie
n556=  aline @Milioner 6inne formy quizow (Redhead, Autoquiz itp.), a najlepiej wyladowac je z 
n557=  aline @Milioner 6pamieci komenda /unload. Uniemozliwi to wykorzystanie przez pomylke 
n558=  aline @Milioner 6komend z tych quizow, co spowodowaloby bledy w aktualnie prowadzonym
n559=  aline @Milioner 6Quizie Milionerowym.
n560=  aline @Milioner 6Jezeli macie inne pytania lub sugestie: 12http://www.quizpl.net
n561=}
n562=
n563=/mzasady {
n564=  msg %ml_quiz_channel 2,2 .8,2Punktowanie: pierwszy odpowiadajacy za dobra odpowiedz 11,2+3 pkt8,2, $&
n565=    za zla 4,2-3 pkt8,2, drugi odpowiadajacy odpowiednio 11,2+/4,2-2 pkt8,2, pozostali 11,2+/4,2-1 pkt8,2, $&
n566=    jesli poprawnie odpowiedziala tylko 1 osoba: BONUS 11,2+1 pkt8,2.
n567=  msg %ml_quiz_channel 2,2 .8,2Poprawna odpowiedz oznacza, iz w nastepnym pytaniu punkty mnozone sa 11,2razy 2 (COMBO)8,2, $&
n568=    dwie i wiecej poprawnych odpowiedzi z rzedu powoduje mnozenie punktow w nastepnych pytaniach 11,2razy 3 (TRIMBO)8,2. $&
n569=    Uwaga! Mnozone sa punkty tak dodatnie jak i ujemne 4,2(!)8,2, a wiec mozna za pytanie zdobyc11,2 12 pkt8,2 $&
n570=    lub stracic4,2 9 pkt8,2; niepoprawna odpowiedz lub brak odpowiedzi kasuje mnoznik.
n571=  if (%ml_points_for_answers == 1) {
n572=    msg %ml_quiz_channel 2,2 .8,2Tryb punkt za odpowiedz oznacza, iz za kazda udzielona odpowiedz, bez wzgledu na to czy poprawna $&
n573=      czy bledna, gracz uzyskuje dodatkowo 11,2+1 pkt8,2.
n574=  }
n575=}
n576=
n577=/mkanal {
n578=  set %ml_quiz_channel $1
n579=  echo 9,1 MILIONEROWY 08Quiz:8,2 Quiz przeniesiony na kanal 0,1 %ml_quiz_channel 2,2 $adjust_length(34, %ml_quiz_channel)
n580=}
n581=
n582=; player, points
n583=/mpunkt {
n584=  var %points 1
n585=  if ($2 != $null) {
n586=    %points = $2
n587=  }
n588=
n589=  var %total_player_points %points
n590=  if ($hget(total_points, $1)) {
n591=    inc %total_player_points $hget(total_points, $1)
n592=  }
n593=  hadd -m total_points $1 %total_player_points
n594=
n595=  echo 9,1 MILIONEROWY 08Quiz:8,2 Dodalem 0,1 $1 8,2 %points $get_point_word(%points) 02,02 $&
n596=    $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(40, $1)
n597=}
n598=
n599=; czyje, publicznie (1/0)
n600=/mile {
n601=  if ($hget(total_points, $1)) {
n602=    var %points = $hget(total_points, $1)
n603=    var %better_players = 0
n604=    var %i = 1
n605=    while ($hget(total_points, %i).item) {
n606=      if ($hget(total_points, %i).data > %points) {
n607=        inc %better_players
n608=      }
n609=      inc %i
n610=    }
n611=
n612=    var %place = %better_players + 1
n613=    if ($2 == 1) {
n614=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jest na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n615=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(25, $1)
n616=    } 
n617=    else {
n618=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 jestes na %place $+ . miejscu ( $+ %points $get_point_word(%points) $+ ) 02,02$&
n619=        $adjust_length(3, %place) $+ $adjust_length(3, %points) $+ $adjust_length(7, $get_point_word(%points)) $+ $adjust_length(23, $1)
n620=    }
n621=  } 
n622=  else {
n623=    if ($2 == 1) {
n624=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie ma jeszcze zadnego punktu2,2 $adjust_length(31, $1)
n625=    } 
n626=    else {
n627=      .notice $1 9,1 MILIONEROWY 08Quiz:08,02 00,01 $1 08,02 nie masz jeszcze zadnego punktu2,2 $adjust_length(29, $1)
n628=    }
n629=  }
n630=}
n631=
n632=/mpauza {
n633=  if (%ml_number_of_questions != $null) {
n634=    if (%ml_running == 1) {
n635=      set %ml_running 0
n636=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wstrzymana 2,2.................................................
n637=    }
n638=    else { 
n639=      msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 gra wznowiona 2,2..................................................
n640=      set %ml_running 1
n641=      next_question 
n642=    }
n643=  }
n644=}
n645=
n646=//mwersja {
n647=  msg %ml_quiz_channel 1,1................9,1 MILIONEROWY 8Quiz (wersja %ml_version $+ ) by 0KrzS&Dizzy&wilk 1,1................
n648=  msg %ml_quiz_channel 2,2.......................8,2 Sciagaj z: 0http://www.quizpl.net2,2 ..........................
n649=}
n650=
n651=/mczas { 
n652=  set %ml_answer_time $1
n653=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas na odpowiedz: 0,1 %ml_answer_time 8,2 sekund(y)2,2 $adjust_length(3, %ml_answer_time) $+ .............................
n654=  set %ml_answer_time %ml_answer_time - 10
n655=}
n656=
n657=/minterwal { 
n658=  set %ml_question_delay $1 
n659=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Czas pomiedzy pytaniami: 0,1 %ml_question_delay 8,2 sekund(y)2,2 $adjust_length(3, %ml_question_delay) $+ .......................
n660=}
n661=
n662=/mpodp { 
n663=  msg %ml_quiz_channel 9,1 MILIONEROWY 08Quiz:8,2 Podpowiedz: 0,1 $1- 2,2 $adjust_length(49, $1-)
n664=}
n665=
n666=//mblokuj {
n667=  hadd -m locked_players $1 1
n668=  var %reason $2-
n669=  if (%reason == $null) {
n670=    %reason = nie podano
n671=  }
n672=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Odpadasz z gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n673=}
n674=
n675=//modblokuj {
n676=  hdel locked_players $1
n677=  var %reason $2-
n678=  if (%reason == $null) {
n679=    %reason = nie podano
n680=  }
n681=  .notice $1 9,1 MILIONEROWY 08Quiz:8,2 Wracasz do gry. Powod: 0,1 %reason 2,2 $adjust_length(38, %reason)
n682=}
